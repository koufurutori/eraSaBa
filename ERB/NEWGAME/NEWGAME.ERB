;-----------------------------------------------------------
;처음부터
;-----------------------------------------------------------
@NEWGAME
게임시작처리중 = 1
;본체버전을 기록
SAVESTR:본체버전 = %eraTW_Version%
;클래스 변수 초기화
CALL MAKE_ALL_CLASS_LIST
;Default 옵션
CALL DEFAULT_OPTION

PBAND = 12

;일자 1 일째
DAY = 1
DAY:2 = 1
DAY:3 = 1

VAR_FERMENT = 그냥 물
;(天候パッチ)冷害発生回数・風害発生回数・水害発生回数・連続降水カウンターをリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
CONTINUOUS_RAINFALL = 0
;(天候パッチ)降雪・雪崩判定を解除
CONTINUOUS_SNOWFALL = 0
SNOW_COVERAGE = 0
POSSIBLITY_AVALANCHE = 0
;(天候パッチ)時間帯をリセット
WEATHER_TIMEZOME = 0
;(天候パッチ)長期天候傾向をリセット
YEAR_WEATHER_CONTROL = 0
;(天候パッチ)短期予報を行う
CALL WEATHER_FORECAST_1TERM
;(天候パッチ)1日予報を行う
CALL WEATHER_FORECAST_1DAY

;캐릭터 생성
CALL ADD_ALL_CHARACTERS
;MASTER생성
CALL NEWGAME_MAKE

;애들 스트레스 수치 리셋
FOR I,0,CHARANUM
	BASE:I:스트레스 = 0
NEXT


;이사 기능(SET_MAINHOME) 꺼뒀으니 따로 여기서 설정해둠.
DEFAULT초기위치 = P5에마・히로방

IF 게임시작처리중 || 게임인계처리중
	MAIN_MAP = 0
	TRYCALLFORM ROOMSETTING_{MAIN_MAP}
ENDIF

;주거설정
;이사 기능을 꺼두기 위해서 함수 CALL 막아뒀다.
;CALL SET_MAINHOME

;옵션
CALL NEWGAME_OPTION

;캐릭터 설정
FOR LOCAL,0,CHARANUM
	;개시 위치
	CFLAG:LOCAL:현재위치 = CFLAG:LOCAL:초기위치
	;복장
	CALL SELECT_CLOTHES(LOCAL, "오늘의 의상")
	CALL CTRL_CLOTHES_SET(LOCAL, "현재 의상의 변경_평상복")
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	CALL 속옷확인리셋(LOCAL)
	CVARSET BASE, GETNUM(BASE, "술기운"), 0
NEXT
;MASTER기상시간 세트(6시)
TIME:3 = 360
;MASTER수면중 플래그ON
CALL SET_SLEEP(1,MASTER,0)
게임시작처리중 = 0


;경고장 모드를 기본적으로 사용하게 만들어둘까?
FLAG:경고장사용 = 1


;유키부터 마녀화까지 전부 삭제
FOR I,15,28
	CALL MANOSABA_DELETED(I)
NEXT

;시작부터 낙오자발생은 ㄴㄴ
FLAG:낙오자발생 = 0

[IF_DEBUG]
REPEAT CHARANUM
    PRINTFORML {COUNT}番目に %NAME:COUNT%。
REND
[ENDIF]


RETURN 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1.00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;캐릭터를 추가했을 때에 하나 하나 여기를 만질 필요가 없게
;캐릭터수상한은 ERH 파일로 정의
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS
FOR LOCAL,CHARANUM,캐릭터수상한
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	CALL SET_MINIMUM_EXP_ALL(LOCAL)
	CALL KOJO_MULTIPLE(LOCAL)
	IF !EXP:LOCAL:키스경험
		TALENT:LOCAL:키스미경험 = 1
	ENDIF
NEXT


;-----------------------------------------------------------
;설정 변경
;-----------------------------------------------------------
@NEWGAME_OPTION
#DIMS choices, 100
;초기화
;무능력자, 농민
SETBIT FLAG:1000, 1
SETBIT FLAG:1000, 2
;TINKO
SIF !TALENT:MASTER:형상
	TALENT:MASTER:형상 = 3
;복장
SIF !CFLAG:MASTER:기본복장세트
	CALL MASTER_CLOTHES_INIT
;
SIF !게임시작처리중
	뉴게임특전 '= "없음"
;선택지
VARSET choices
PRINTL
PRINTL
PRINTL 게임 개시 전에 각종 설정을 변경할 수 있습니다.
DRAWLINE
;당신설정
SIF !FLAG:역극
	choices:0 '= @"%마스터를% 커스터마이즈한다"

choices:2 '= @"%CALLNAME:MASTER%의 복장은 %MASTER_CLOTHES_NAME(CFLAG:MASTER:기본복장세트)%이다"
IF FLAG:생활능력무
	choices:3 '= @"%CALLNAME:MASTER%에겐 고백할만한 담력이 없다"
ELSE
	choices:3 '= @"%CALLNAME:MASTER%의 담력은 보통이다"
ENDIF


IF FLAG:난이도상승
	choices:4 '= @"%CALLNAME:MASTER%는 불합리한 상황에 놓인 모양이다……"
ELSE
	choices:4 '= @"%CALLNAME:MASTER%는 본래 처했을 법한 상황에 있다"
ENDIF



choices:5 '= @"%마스터는% %GET_MAPNAME(MAIN_MAP)%의 %STR:(DEFAULT초기위치+8000)%에서 눈을 뜹니다"

; 미 친 처 녀 설 정 능 력 진 짜
choices:10 '= @"각 캐릭터의 처녀・비처녀를 변경할 수 있습니다"

;체형 바꾸기는 꺼두자고
;choices:11 '= @"각 캐릭터의 체형을 변경할 수 있습니다"

;특전
IF 게임시작처리중
	choices:20 '= NEWGAMEBONUS_TEXT()
ELSEIF 게임인계처리중
	choices:20 '= 주회특전_표시()
ENDIF

CALL ASK_CHOICES(choices, 100, "종료하고 시작한다")
SELECTCASE RESULT
	CASE 0
		IF !FLAG:역극
			CALL CUSTOM_TERMINAL(MASTER)
			;이사 기능을 꺼두기 위해서 함수 CALL 막아뒀다.
			;CALL SET_MAINHOME
		ENDIF
	CASE 100
		CALL VIRGIN_SET_APPLY
		CALL NEWGAME_BONUS_APPLY
		;CALL 주회특전_적용
		ITEM:스마트폰 = 1
		RETURN
	CASE 2
;옷 갈아입는 거 막기 위해 CALL 막아야 하나?
	;	CALL MASTER_CLOTHES
	CASE 3
		CALL KAISYONASI
	CASE 4
		CALL HARDERMODE
	CASE 5
;이사 기능을 꺼두기 위해서 함수 CALL 막아뒀다.
		;CALL SET_MAINHOME
	CASE 10
		CALL VIRGIN_SET
	;CASE 11
		;CALL BODY_SET
	CASE 20
		IF 게임시작처리중
			CALL NEWGAME_BONUS_SELECT
		ELSEIF 게임인계처리중
			CALL 주회특전
		ENDIF
ENDSELECT
RESTART

;-----------------------------------------------------------
;설정 변경
;-----------------------------------------------------------
@NEWGAME_VERUP
;연인수정이 끝난 플래그를 세운다
;KR판은 연인이 구버전 일판 기반 플래그이므로 주석처리
; SETBIT FLAG:1000, 3
; SETBIT FLAG:1000, 4
CALL VERSION_UP
PRINTW 게임 개시처리 종료
SAVEDATA 99, "처음부터"
PRINTL 자동으로 저장되었습니다.
PRINTW 게임 업데이트 처리로 넘어가시겠습니까?
CALL ASK_YN()
IF !RESULT
	CALL UPDATE
ELSE
	PRINTL 업데이트를 건너뛰었습니다.
	PRINTW 기상 전의 SHOP 화면에서 업데이트 해주세요
ENDIF