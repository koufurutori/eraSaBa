@UPDATE
#DIM C_ID
#DIM 갱신능력번호
#DIM 최저능력치
#DIM 최저경험수
#DIM 대사실장체크, 캐릭터수상한
#DIM 리셋화면
#DIM 기능소질갱신용
#DIMS rname

PRINTL 구버전에는 존재하지 않던 캐릭터·초기 소질·플래그 등이 있으면 수중에 넣습니다.

VARSET 대사실장체크
리셋화면 = 0


FOR C_ID,1,CHARANUM ;CSV의 이름을 참고해 NO를 고친다
	VARSET LOCAL
	FOR LOCAL,1,캐릭터수상한
		SIF !EXISTCSV(LOCAL)
			CONTINUE
		IF NAME:C_ID == CSVNAME(LOCAL)
			NO:C_ID = LOCAL
			BREAK
		ENDIF
	NEXT
NEXT

FOR C_ID,1,캐릭터수상한 ;끝나면 새로운 캐릭터 추가
	; !FLAG:(3000+C_ID) -> 캐릭터가 일부러 삭제된 게 아닐 때
	IF GETCHARA(C_ID) == -1 && EXISTCSV(C_ID) && !FLAG:(3000+C_ID)
		ADDCHARA C_ID
		TARGET = C_ID
		; 한판 임의수정, 기본 키스경험이 없을 경우, 키스미경험 처리 추가
		SIF !EXP:C_ID:키스경험
			TALENT:C_ID:키스미경험 = 1
		RESULT = 0
	ENDIF

NEXT

FOR C_ID, 1, CHARANUM
	IF NO:C_ID != C_ID && NO:C_ID != 0 ;만약 어긋나면 다시 열거한다
		IF NO:C_ID > C_ID ;CSVNO가 지금의 NO보다 뒤라면 SWAP해서 계속
			SWAPCHARA C_ID,NO:C_ID
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
		ELSE
			SWAPCHARA C_ID,NO:C_ID ;그렇지 않으면 SWAP해서 처음부터 다시 나열한다
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
			C_ID = 1
		ENDIF
	ENDIF

NEXT

SIF !CM_LIMIT
	CM_LIMIT = 100
YOGORE:348 = 0
FOR C_ID, 1, CHARANUM

	;CSTR:일정보를 CSTR:3으로 이행하는 수속
	IF CSTR:C_ID:3 == "" && CSVCSTR(C_ID, 3) != ""
		CSTR:C_ID:3 = %CSVCSTR(C_ID, 3)%
		CSTR:C_ID:1 = 
	ENDIF
	TALENT:C_ID:186 = 0
	IF TALENT:C_ID:연모
		TALENT:C_ID:사모 = 0
		TALENT:C_ID:섹프 = 0
		TALENT:C_ID:애욕 = 0
	ENDIF
	CALL 이력캐릭터라인변환(C_ID)

	SIF !CFLAG:C_ID:의뢰상황
		CFLAG:C_ID:의뢰내용 = 0
	;고유요리플래그
;	SIF !LEARNED_MENU:C_ID
;		LEARNED_MENU:C_ID = CFLAG:C_ID:고유요리
	
	FOR LOCAL:1, 0, 3
		SIF !EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1) = 1 + RAND:3
		SIF !EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1) = 1 + RAND:3
	NEXT

	;종족계 소질을 갱신
	FOR LOCAL:1, 190, 198
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	;수서, 빙정, 종족(미사용? ), 반백택, 동물귀도 변동하지 않는다고 생각되므로 갱신
	FOR LOCAL:1, 163, 167
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	
	;技能系素質,移行完了
;	기능소질갱신용 = MAX(ABL:C_ID:낚시기능, CSVTALENT(C_ID, 47))
	TALENT:C_ID:낚시Lv = MAX(TALENT:C_ID:낚시Lv, CSVTALENT(C_ID, 47))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "낚시경험"), TALENT:C_ID:낚시Lv)
	
;	기능소질갱신용 = MAX(ABL:C_ID:채집기능, CSVTALENT(C_ID, 48))
	TALENT:C_ID:채집Lv = MAX(TALENT:C_ID:채집Lv, CSVTALENT(C_ID, 48))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "채집경험"), TALENT:C_ID:채집Lv)
	
;	기능소질갱신용 = MAX(ABL:C_ID:조제기능, CSVTALENT(C_ID, 49))
	TALENT:C_ID:조제Lv = MAX(TALENT:C_ID:조제Lv, CSVTALENT(C_ID, 49))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "조제경험"), TALENT:C_ID:조제Lv)

;	기능소질갱신용 = CSVTALENT(C_ID, 46)
	TALENT:C_ID:벌채Lv = MAX(TALENT:C_ID:벌채Lv, CSVTALENT(C_ID, 46))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "벌채경험"), TALENT:C_ID:벌채Lv)

	TALENT:C_ID:통각 = CSVTALENT(C_ID, GETNUM(TALENT, "통각"))
	TALENT:C_ID:동물귀 = CSVTALENT(C_ID, GETNUM(TALENT, "동물귀"))
	SIF TALENT:C_ID:회복속도 < CSVTALENT(C_ID, GETNUM(TALENT, "회복속도")) 
		TALENT:C_ID:회복속도 = CSVTALENT(C_ID, GETNUM(TALENT, "회복속도"))
	;아마 괜찮다고 생각하지만, 후천적인 취득 처리를 전망해 판정
	SIF TALENT:C_ID:청소부 == 0
		TALENT:C_ID:청소부 = CSVTALENT(C_ID, GETNUM(TALENT, "청소부"))
	SIF TALENT:C_ID:음감 == 0
		TALENT:C_ID:음감 = CSVTALENT(C_ID, GETNUM(TALENT, "음감"))
	SIF TALENT:C_ID:음악지식 == 0
		TALENT:C_ID:음악지식 = CSVTALENT(C_ID, GETNUM(TALENT, "음악지식"))
	SIF GETBIT(TALENT:C_ID:성별, 1) && !TALENT:C_ID:형상
		CALL TINKO_DECIDE(C_ID)

	;내방확률 보정을 읽어들여
	FOR LOCAL:1, 420, 432
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	;내방 위치를 읽어들여
	FOR LOCAL:1, 450, 462
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	FOR LOCAL:1, 1, CHARANUM
		RELATION:C_ID:(LOCAL:1) = CSVRELATION(C_ID, LOCAL:1)
	NEXT
	;교체 이미지 유무 확인
	IF CFLAG:C_ID:별도얼굴있음 < CSVCFLAG(C_ID, 562) || CFLAG:C_ID:별도스탠딩있음 < CSVCFLAG(C_ID, 563)
		PRINTFORML %CALLNAME:C_ID%의 새로운 대체화상을 검출했습니다。
		IF CFLAG:C_ID:별도얼굴있음 < CSVCFLAG(C_ID, 562)
			CALL 画像セット(@"顔絵_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "デフォルト顔絵")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 562)+1
				CALL 画像セット(@"\@LOCAL:1 >= 2?別顔{LOCAL:1}#別顔\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え顔絵")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		IF CFLAG:C_ID:별도스탠딩있음 < CSVCFLAG(C_ID, 563)
			CALL 画像セット(@"立ち絵_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "デフォルト立ち絵")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 563)+1
				CALL 画像セット(@"\@LOCAL:1 >= 2?別立ち{LOCAL:1}#別立ち\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え立ち絵")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		PRINT [0] 대체없음 
		FOR LOCAL:1, 1, MAX(CSVCFLAG(C_ID, 562), CSVCFLAG(C_ID, 563)) + 1
			PRINTFORM [{LOCAL:1}] 대체{LOCAL:1} 
		NEXT
		INPUT
		CFLAG:C_ID:대체적용 = RESULT
	ENDIF
	CFLAG:C_ID:별도얼굴있음 = CSVCFLAG(C_ID, 562)
	CFLAG:C_ID:별도스탠딩있음 = CSVCFLAG(C_ID, 563)
	;주인 플래그

	CFLAG:C_ID:고유커맨드 = CSVCFLAG (C_ID, GETNUM(CFLAG, "고유커맨드"))
	CFLAG:C_ID:지위 = CSVCFLAG (C_ID, GETNUM(CFLAG, "지위"))
	CFLAG:C_ID:초기위치 = CSVCFLAG (C_ID, GETNUM(CFLAG, "초기위치"))
	SIF CFLAG:C_ID:신사거주
		CFLAG:C_ID:초기위치 = CFLAG:C_ID:신사거주
	CFLAG:C_ID:내방시간 = CSVCFLAG (C_ID, GETNUM(CFLAG, "내방시간"))
	CFLAG:C_ID:귀가시간 = CSVCFLAG (C_ID, GETNUM(CFLAG, "귀가시간"))
	CFLAG:C_ID:취침시간 = CSVCFLAG (C_ID, GETNUM(CFLAG, "취침시간"))
	CFLAG:C_ID:기상시간 = CSVCFLAG (C_ID, GETNUM(CFLAG, "기상시간"))
	CFLAG:C_ID:자주가는지역 = CSVCFLAG (C_ID, GETNUM(CFLAG, "자주가는지역"))
	CFLAG:C_ID:자택위치 = CSVCFLAG (C_ID, GETNUM(CFLAG, "자택위치"))
	CFLAG:C_ID:이동율보정 = CSVCFLAG (C_ID, GETNUM(CFLAG, "이동율보정"))
	CFLAG:C_ID:이동절도 = CSVCFLAG (C_ID, GETNUM(CFLAG, "이동절도"))
	CFLAG:C_ID:내방억제치 = CSVCFLAG(C_ID, GETNUM(CFLAG, "내방억제치"))

	;최대 기력 체력이 CSV치보다 낮은 경우 CSV에 맞춘다
	IF !CFLAG:C_ID:회복속도다운
		SIF MAXBASE:C_ID:0 < CSVBASE (C_ID, 0)
			MAXBASE:C_ID:0 = CSVBASE (C_ID, 0)
		SIF MAXBASE:C_ID:1 < CSVBASE (C_ID, 1)
			MAXBASE:C_ID:1 = CSVBASE (C_ID, 1)
	ENDIF

	;술내성
	MAXBASE:C_ID:술기운 = CSVBASE(C_ID, GETNUM(BASE, "술기운"))
	CALL SET_MINIMUM_EXP_ALL(C_ID)
	FOR 갱신능력번호, 0, 111
		SIF EXP:C_ID:갱신능력번호 < CSVEXP (C_ID, 갱신능력번호)
			EXP:C_ID:갱신능력번호 = (EXP:C_ID:갱신능력번호 + CSVEXP (C_ID, 갱신능력번호))
	NEXT
	;팬티 종류 비존재시 소거
	FOR LOCAL:1, 2, MAXPANTS
		SIF CFLAG:C_ID:(LOCAL:1 + 팬티관리) && GET_STR(C_ID, "하반신속옷_비켜놓기가능", LOCAL:1, "이름") == ""
			CFLAG:C_ID:(LOCAL:1 + 팬티관리) = 0
	NEXT

	;선물 정보의 수정
	IF !GIFT_GOT:C_ID:1
		CFLAG:C_ID:선물스톡 = 0
		IF CFLAG:C_ID:받은선물
			CFLAG:C_ID:받은선물 -= (GET_GIFTDATA(CFLAG:C_ID:받은선물, "횟수") * 10000000000000000)
			CFLAG:C_ID:받은선물 += 1 * 10000000000000000
			GIFT_GOT:C_ID:1 = CFLAG:C_ID:받은선물
		ENDIF
	ENDIF


	;이 아래의 데이터 오류 검사는 TW버전에서 외출맵이 있었을 떼 거기서 어디에 나오는가 판정으로 본다
	;즉, 지금 여기서는 쓸모 없음. 그러니 전체 주석처리하고자 함.
	[SKIPSTART]
	;캐릭터 데이터 오류 검사
	LOCAL:4 = 0
	FOR LOCAL:1, 0, MAXHOME
		FOR LOCAL:2, 1, 10
			LOCAL:3 = LOCAL:1 * 100 + LOCAL:2 * 10
			CALLFORM CHARAMOVE_DATA_{C_ID}(7, LOCAL:3)
			IF RESULT
				LOCAL:4 = 1
				BREAK
			ENDIF
		NEXT
		SIF LOCAL:4
			BREAK
	NEXT
	IF !LOCAL:4
		PRINTFORML ※%CALLNAME:C_ID%({C_ID})의 CHARAMOVE_DATA에 문제가 있습니다.
		THROW
	ENDIF
	[SKIPEND]


NEXT
CFLAG:MASTER:신사거주 = CFLAG:MASTER:초기위치

SIF VAR_FERMENT == "" || VAR_FERMENT == "0"
	VAR_FERMENT = 그냥 물




;의상 플래그의 수정
FOR I,1,CHARANUM
	CFLAG:I:기본복장세트 = (100+I)
NEXT


;CFLAG:[[에마]]:기본복장세트 = 101
;CFLAG:[[안안]]:기본복장세트 = 102
;CFLAG:[[노아]]:기본복장세트 = 103
;CFLAG:[[레이아]]:기본복장세트 = 104
;CFLAG:[[미리아]]:기본복장세트 = 105
;CFLAG:[[마고]]:기본복장세트 = 106
;CFLAG:[[나노카]]:기본복장세트 = 107
;CFLAG:[[아리사]]:기본복장세트 = 108
;CFLAG:[[셰리]]:기본복장세트 = 109
;CFLAG:[[한나]]:기본복장세트 = 110
;CFLAG:[[코코]]:기본복장세트 = 111
;CFLAG:[[메루루]]:기본복장세트 = 112
;CFLAG:[[고쿠쵸]]:기본복장세트 = 113
;CFLAG:[[간수]]:기본복장세트 = 114
;CFLAG:[[유키]]:기본복장세트 = 115



;당신 관련 처리

;본래 붙지 않을 부정적 감정이 MASTER에게 붙어 있는 경우의 삭제처리
SIF CFLAG:MASTER:언짢음 == 1
	CFLAG:MASTER:언짢음 = 0
SIF CFLAG:MASTER:빡침 == 1
	CFLAG:MASTER:빡침 = 0
SIF TALENT:MASTER:기분 == -1
	TALENT:MASTER:기분 = 0


MAXBASE:MASTER:술기운 = 1500
;MASTER가 여자, 후타나리 때
IF GETBIT(TALENT:MASTER:성별, 0)
	IF FLAG:주회수 && TALENT:MASTER:처녀 == 1 && CSTR:MASTER:처녀상실이력 != ""
		CSTR:MASTER:처녀상실이력 = 
		CFLAG:MASTER:처녀상실기념일 = 0
		CFLAG:MASTER:처녀상실대상 = 0
	ENDIF
;MASTER가 남자
ELSE
	TALENT:MASTER:가슴사이즈 = -2
ENDIF
SIF GETBIT(TALENT:MASTER:성별, 1) && !TALENT:MASTER:형상
	CALL TINKO_DECIDE(MASTER)
;요술망치의 대상이 종료하지 않고 훨씬 허술한 쓸모 있게 되고 있는 경우의 수정
;요주의 크게 한 후에 2 개로 했을 경우, 및 2 개로 한 후에 크게 했을 경우, 허술한 것이 Default가 될 가능성이 있습니다
;그 경우, 스스로 원래 형상을 재설정하지 않으면 안됩니다


IF TALENT:MASTER:체형 == -3
	TALENT:MASTER:체형 = 0
	PRINTFORML ―――%CALLNAME:MASTER%의 체형을 재설정합니다―――
	PRINTL 
ENDIF

;접는우산
SIF ITEM:36 == 0 && ITEMSTOCK(36) == 2
	ITEMSALES:36 ++
;로터
SIF ITEM:0 < 0
	ITEM:0 = 0
SIF ITEM:0 == 0 && ITEMSTOCK(0) == 2
	ITEM:0 = 1

FLAG:출금인원수 = 0
FOR C_ID, 1, CHARANUM

	SIF CFLAG:C_ID:출금
		FLAG:출금인원수 ++
NEXT

SIF FLAG:3 == 0
	PRINTL 연회의유무 옵션이 OFF가 되었습니다

LOCAL = TARGET
FOR C_ID, 1, CHARANUM
	RESULT = 0
	TARGET = C_ID
	CALL KOJO_MULTIPLE(C_ID)
	IF RESULT
		;신규구상 추가시의 리셋 처리 스위치
		IF RESULT == 2
			대사실장체크 :C_ID = 1
			리셋화면 = 1
		ENDIF
		RESULTS =
		;구상의 존재 판정 and RESULTS에 문자열 대입
		IF CFLAG:C_ID:구상셀렉터
			TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{C_ID}
		ENDIF
		PRINTFORML %CALLNAME:C_ID%%RESULTS% 구상의 UPDATE가 존재하는지 확인합니다
		TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{C_ID}
		PRINTL 
	ENDIF
NEXT
TARGET = LOCAL

SIF FLAG:개시일 < DAY
	FLAG:개시일 = DAY + 1
VARSET FLAG, 0, 300, 500
VARSET LOCAL
;팬티를 CFLAG에 이행
;SAVESTR:10에 2 문자 이상 있으면 실행
IF STRLENS(SAVESTR:10) > 1
	FLAG:700 = 0
	FOR LOCAL,1,CHARANUM
		FOR LOCAL:1,0,MAXPANTS
			LOCALS = /%CALLNAME:LOCAL%{LOCAL:1}/
			CFLAG:LOCAL:(LOCAL:1 + 팬티관리) = STRCOUNT(SAVESTR:10,LOCALS)
			SIF CFLAG:LOCAL:(LOCAL:1 + 팬티관리)
				FLAG:700 ++
		NEXT
	NEXT
	SAVESTR:10 =
	PRINTL 당신의 중요한 팬티 콜렉션을 이행했습니다
ENDIF



PRINTFORML 낡은 세이브 데이터를 계승했을 경우, 의뢰 상황을 리셋하지 않으면 에러로 다운될 가능성이 높습니다.
PRINTFORML 의뢰 상황을 리셋하겠습니까?
CALL ASK_YN
SIF !RESULT
	CALL IRAI_RESET


;아이템류의 재고 관리
FOR LOCAL, 0, 1000
	;ついでに소지수제한,換金はしません
	IF ITEM:LOCAL > 소지수제한 && !GROUPMATCH(ITEMNAME:LOCAL, "마음에드는아이템")
		ITEM:LOCAL = 소지수제한
		PRINTFORMW %ITEMNAME:LOCAL%が소지수제한を超えていたので制限値まで減らしました
	ENDIF
	;ついでのついでに存在外アイテムの削除
	IF ITEMNAME:LOCAL == ""
		ITEM:LOCAL = 0
		ITEMSALES:LOCAL = 0
	ENDIF
NEXT

IF TALENT:MASTER:체형 == -3
	TALENT:MASTER:체형 = 0
	PRINTFORML ---%CALLNAME:MASTER%의 체형을 재설정합니다―--
	PRINTL 
ENDIF
IF !FLAG:연회개최플래그
ELSEIF !FLAG:32
	;필요한가 모르겠지만 만약을 위해, 입니다
	;잠정 처리로 우선 라운지를 지정, 주최자가 어긋나거나 해도 신경쓰지 말아라
	FLAG:32 = 32
	PRINTFORML 연회의 개최 위치를 수정합니다
ELSEIF FLAG:32 == 230
	;버그 보고에서의 FLAG:32의 내용은 아마 여기
	FLAG:32 = 32
	PRINTFORML 연회의 개최 위치를 수정합니다
ENDIF

CALL SET_ENKAI_LOCATIONDISPLAY
SAVESTR:본체버전 = %eraTW_Version%

IF 리셋화면
	PRINTFORML 이하 캐릭터의 대사가 신규 실장되었습니다.
	PRINTFORML 호감도, 경험, 능력 등을 리셋하고 싶은 캐릭터를 선택해주세요.
	$PRINT_RESETCHARA
	FOR LOCAL, 1, CHARANUM
		SIF !대사실장체크 :LOCAL
			CONTINUE
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTFORML 
	DRAWLINE
	PRINTFORML [999] 종료
	INPUT
	SIF 캐릭터수상한 < RESULT
		GOTO UPDATE_END
	SIF !대사실장체크 :RESULT
		GOTO UPDATE_END
	IF 대사실장체크 :RESULT
		대사실장체크 :RESULT = 0
		CALL RESET_KOJO_CHARA(RESULT)
		GOTO PRINT_RESETCHARA
	ENDIF
ENDIF

$UPDATE_END

PRINTL 
;거점 맵의 재설정
IF CFLAG:MASTER:현재위치 != OMANEKIBEYA() && !FLAG:기절중단
	CALL MAP_DEFAULTRESIDENT(MAIN_MAP, 1)
	PRINTFORML 거점 맵의 설정을 재설정했습니다
ELSE
	CALL COLORMESSAGE(@"※거점맵의 설정을 재설정할 수 없었습니다", C_YELLOW, 1)
	IF CFLAG:MASTER:현재위치 == OMANEKIBEYA()
		PRINTL 이유：초대받은 중
	ELSEIF FLAG:기절중단
		PRINTL 이유：기절 중
	ENDIF
	FORCEWAIT
ENDIF

PRINTW 갱신 완료했습니다

;업데이트로 CSV read에 의한 갱신되지 않는 소질
@TALENT_NOT_UPDATED(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 15,28,29,35 TO 39, 42 TO 49, 58, 59, 63 TO 69, 72 TO 79, 87 TO 91, 95 TO 99, 107 TO 120, 122 TO 129
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@이력캐릭터라인변환(ARG)
#DIM 일자
VARSET LOCALS
IF CFLAG:ARG:첫키스상실기념일 && CSTR:ARG:첫키스상실이력 == ""
	일자 = CFLAG:ARG:첫키스상실기념일
	LOCALS = %PRINT_DATE_F(일자)%, %CALLNAME:(CFLAG:ARG:첫키스상실상대)%에
	SIF CSTR:ARG:첫키스위치 != ""
		LOCALS += @"%CSTR:ARG:첫키스위치%로"
	IF CFLAG:ARG:첫키스상실대상 & (상실_스스로 | 상실_연모 | 상실_사모)
		LOCALS += "첫키스를 바쳤다"
	ELSE
		SIF CFLAG:ARG:첫키스상실대상 & 상실_어영부영
			LOCALS += "흘러가는 대로"
		LOCALS += "첫키스를 빼앗겼다"
	ENDIF
	CSTR:ARG:첫키스상실이력 = %LOCALS%
	PRINTFORML %CALLNAME:ARG%의 첫키스 이력을 신형식으로 변환했습니다
ENDIF

IF CFLAG:ARG:애널처녀상실기념일 && CSTR:ARG:애널처녀상실이력 == ""
	일자 = CFLAG:ARG:애널처녀상실기념일
	LOCALS:1 = %PRINT_DATE_F(일자)%,\@ CFLAG:ARG:애널처녀상실대상 & 상실_시간정지?  시간정지중# \@
	SIF CSTR:ARG:A처녀상실위치 != ""
		LOCALS:1 += @"%CSTR:ARG:A처녀상실위치%로"
	IF GETBIT (CFLAG:ARG:애널처녀상실대상,3)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:애널처녀상실상대)%에 페니스밴드로"
	ELSEIF GETBIT (CFLAG:ARG:애널처녀상실대상,2)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:애널처녀상실상대)%에 애널바이브로"
	ELSEIF GETBIT (CFLAG:ARG:애널처녀상실대상,1)
		LOCALS:1 += @"%CALLNAME:(CFLAG:ARG:애널처녀상실상대)%의 페니스에"
	ENDIF
	IF CFLAG:ARG:애널처녀상실대상 & 상실_시간정지
		LOCALS:1 += "항문 처녀를 빼앗겼다"
	ELSE
		LOCALS:1 += "항문 처녀를 바쳤다"
	ENDIF
	CSTR:ARG:애널처녀상실이력 = %LOCALS:1%
	PRINTFORML %CALLNAME:ARG%의 애널처녀상실이력을 신형식으로 변환했습니다
ENDIF

IF CFLAG:ARG:처녀상실기념일 && CSTR:ARG:처녀상실이력 == ""
	일자 = CFLAG:ARG:처녀상실기념일
	LOCAL = CFLAG:ARG:처녀상실대상
	LOCALS:2 = %PRINT_DATE_F(일자)%,
	SIF LOCAL & 상실_시간정지
		LOCALS:2 += "시간정지중에"
	SIF GETBIT (LOCAL,9)
		LOCALS:2 += "만취하고 있을 때"
	SIF GETBIT (LOCAL,16)
		LOCALS:2 += "자고 있는 동안에"
	SIF CSTR:ARG:처녀상실위치 != ""
		LOCALS:2 += @"%CSTR:ARG:처녀상실위치%로\@ LOCAL & 상실_연모 ?  사랑하는# \@%CALLNAME:(CFLAG:ARG:처녀상실상대)%"
	IF GETBIT (LOCAL,3)
		LOCALS:2 += "에 페니스밴드로"
	ELSEIF GETBIT (LOCAL,2)
		LOCALS:2 += "에 바이브로"
	ELSEIF GETBIT (LOCAL,1)
		LOCALS:2 += "의 페니스에"
	ENDIF
	SIF LOCAL & 상실_어영부영
		LOCALS:2 += "조금씩"
	IF (GETBIT (LOCAL, 11) || GETBIT (LOCAL, 12)) && ((!GETBIT (LOCAL,9) && !GETBIT (LOCAL,13) && !GETBIT (LOCAL,16)) || GETBIT (LOCAL,14))
		LOCALS:2 += "처녀를 바쳤다"
	ELSE
		SIF LOCAL & 상실_억지로
			LOCALS:2 += "힘으로"
		LOCALS:2 += @"처녀를 \@ARG == MASTER ? 주었다 # 빼앗겼다\@"
	ENDIF
	CSTR:ARG:처녀상실이력 = %LOCALS:2%
	PRINTFORML %CALLNAME:ARG%의 처녀상실이력을 신 형식으로 변환했습니다
ENDIF

@RESET_KOJO_CHARA(ARG)
CFLAG:ARG:호감도 = 0
CFLAG:ARG:신뢰도 = 0
CFLAG:ARG:약점잡음 = 0
CFLAG:ARG:약점잡힘 = 0
CFLAG:ARG:기정사실 = 0
CFLAG:ARG:낮해금 = 0
CFLAG:ARG:안면 = 0
CFLAG:ARG:퇴짜 = 0
CFLAG:ARG:딸딸이들킴 = 0
CFLAG:ARG:면간발각 = 0
CFLAG:ARG:수면간 = 0
CFLAG:ARG:만취간 = 0
CFLAG:ARG:발렌타인 = 0
CFLAG:ARG:마지막에만난날 = 0

CFLAG:ARG:깨끗한교제 = 0

CFLAG:ARG:술을못함 = 0

CFLAG:ARG:구내애액 = 0
CFLAG:ARG:안면애액 = 0
CFLAG:ARG:손에애액 = 0




MARK:ARG:반발각인 = 0
MARK:ARG:반발취득이력 = 0
MARK:ARG:고통각인 = 0
MARK:ARG:쾌락각인 = 0
MARK:ARG:괘씸각인 = 0


CSTR:ARG:처녀상실이력 =
CSTR:ARG:애널처녀상실이력 =
CSTR:ARG:첫키스상실이력 =

;経験
EX:ARG:분유 = 0
EX:ARG:방뇨 = 0


UWAKI_KNOWN:ARG:0 =
UWAKI_ACCEPTED:ARG:0 =

;함락
TALENT:ARG:약점 = 0
TALENT:ARG:연모 = 0
TALENT:ARG:사모 = 0
TALENT:ARG:음란 = 0
TALENT:ARG:애욕 = 0
TALENT:ARG:섹프 = 0
TALENT:ARG:모성 = 0
TALENT:ARG:연인 = 0
TALENT:ARG:초경 = 0
TALENT:ARG:모유체질 = 0
SIF TALENT:MASTER:연인 == ARG
	TALENT:MASTER:연인 = 0
;음란계
TALENT:ARG:마조 = 0
TALENT:ARG:자위중독 = 0
TALENT:ARG:음호 = 0
TALENT:ARG:항문광 = 0
TALENT:ARG:음유 = 0
TALENT:ARG:키스마 = 0
TALENT:ARG:포르치오성감 = 0

IF CFLAG:ARG:회복속도다운
	MAXBASE:ARG:체력 += 500
	MAXBASE:ARG:기력 += 500
	TALENT:ARG:회복속도 = CFLAG:ARG:회복속도다운 - 2
	CFLAG:ARG:회복속도다운 = 0
ENDIF
IF CFLAG:ARG:가슴랭크업 == 1 && TALENT:ARG:가슴사이즈 > -2
	TALENT:ARG:가슴사이즈 -= 1
	CFLAG:ARG:가슴랭크업 = 0
ENDIF
TALENT:ARG:통각 = CSVTALENT(ARG,40)
CFLAG:ARG:애널처녀상실기념일 = 0
CFLAG:ARG:처녀상실기념일 = 0
CSTR:ARG:애널처녀상실이력 =
CSTR:ARG:처녀상실이력 =
CSTR:ARG:첫키스상실이력 =

FOR LOCAL,0,100
	ABL:ARG:LOCAL = CSVABL(ARG,LOCAL)
	EXP:ARG:LOCAL = CSVEXP(ARG,LOCAL)
NEXT
VARSET JUEL:ARG:0, 0

SIF !EXP:ARG:키스경험
	TALENT:ARG:키스미경험 = 1
TALENT:ARG:0 = CSVTALENT(ARG,0)
PRINTFORMW %ARG를% 리셋 했습니다.



;CSV 갱신용
@SET_MINIMUM_EXP_ALL(C_ID)
#DIM C_ID
#DIM 갱신능력번호
#DIM 최저능력치

FOR 갱신능력번호,0,100
	최저능력치 = CSVABL(C_ID, 갱신능력번호)
	SIF 최저능력치 == 0
		CONTINUE
	;전투능력
	IF ABLNAME:갱신능력번호 == "전투능력"
		;전투능력 수정
		SIF ABL:C_ID:갱신능력번호 < 최저능력치 && EXP:C_ID:전투경험 == CSVEXP(C_ID, GETNUM(EXP, "전투경험"))
			ABL:C_ID:갱신능력번호 = 최저능력치
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "전투경험"), 최저능력치)
		CONTINUE
	ENDIF
	ABL:C_ID:갱신능력번호 = MAX(최저능력치, ABL:C_ID:갱신능력번호)
	SELECTCASE ABLNAME:갱신능력번호
	CASE "청소기능"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "청소경험"), 최저능력치)
	CASE "화술기능"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "회화경험"), 최저능력치)
	CASE "교양"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "학습경험"), 최저능력치)
	CASE "요리기능"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "요리경험"), 최저능력치)
	CASE "음악기능"
		SELECTCASE TALENT:C_ID:음악지식
		CASE 1
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "연주경험"), 최저능력치)
		CASE 2
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "가창경험"), 최저능력치)
		CASE 3
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "무도경험"), 최저능력치)
		ENDSELECT
	;移行完了
	; CASE "낚시기능"
	; 	ABL:C_ID:갱신능력번호 = MAX(ABL:C_ID:갱신능력번호, CSVTALENT(C_ID, 47))
	; 	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "낚시경험"), 최저능력치)
	; 	TALENT:C_ID:낚시Lv = MAX(TALENT:C_ID:낚시Lv, ABL:C_ID:낚시기능)
	; CASE "채집기능"
	; 	ABL:C_ID:갱신능력번호 = MAX(ABL:C_ID:갱신능력번호, CSVTALENT(C_ID, 48))
	; 	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "채집경험"), 최저능력치)
	; 	TALENT:C_ID:채집Lv = MAX(TALENT:C_ID:채집Lv, ABL:C_ID:채집기능)
	; CASE "조제기능"
	; 	ABL:C_ID:갱신능력번호 = MAX(ABL:C_ID:갱신능력번호, CSVTALENT(C_ID, 49))
	; 	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "조제경험"), 최저능력치)
	; 	TALENT:C_ID:조제Lv = MAX(TALENT:C_ID:조제Lv, ABL:C_ID:조제기능)
	ENDSELECT
NEXT


;비에로계 ABL, 40~49에 대해
;CSV 준거로 능력이 상승했을 경우에 최저한의 EXP를 취득해, 표시한다
@SET_MINIMUM_EXP(C_ID, VAR_ID, MINIMUM_ABL)
#DIM C_ID
#DIM VAR_ID
#DIM MINIMUM_ABL
#DIM MINIMUM_EXP
SIF MINIMUM_ABL == 0
	RETURN
MINIMUM_EXP = EXPLV:(MINIMUM_ABL + 2)
SIF EXP:C_ID:VAR_ID >= MINIMUM_EXP
	RETURN
EXP:C_ID:VAR_ID = MINIMUM_EXP
;KR판 임의수정, 민원 많아 해당 기능 디버그용으로 처리
DEBUGPRINTFORML %CALLNAME:C_ID%의 %조사처리(EXPNAME:VAR_ID,"가")% {EXP:C_ID:VAR_ID}로 올랐습니다

;-------------------------------------------------
;CSVを使わず弾幕特殊能力を個別に設定する関数
;新しくキャラクターを追加する際にCSVだと手間なので増設したものです
;▼memo
;0	【言笑自若】: 相手のダイス数Down
;	何があっても決して慌てず、落ち着いていることのたとえ。
;1	【温柔敦厚】: 相手のダイス面Down
;	優しく穏やかで、思いやりがあること。
;2	【勇気凛々】: %CALLNAME:OPPONENT%のダイス数Up
;	失敗や危険をかえりみず、勇敢に物事に立ち向かっていこうとするさま。
;3	【豪放磊落】: %CALLNAME:OPPONENT%のダイス面Up
;	心が広く大らかで、些細なことは気にしないこと。または、そのような様子。
;4	【怪力乱神】: %CALLNAME:OPPONENT%のダイス値が相手のダイス値より小さくても、その差が一定数以下なら、残機が減らない
;	人の知識では理解することが出来ない、怪しく奇怪な現象や物事のこと。
;5	【洽覧深識】: %CALLNAME:OPPONENT%の初期残機が4になる
;	体験して得た経験や知識が多くあり、様々なことを知っていること。
;6	【残忍酷薄】: 相手の被弾からの復帰時のボム数が-1される
;	人のことを気遣うこともなく、むごいこと。
;7	【乾坤一擲】: %CALLNAME:OPPONENT%が一定確率でボムを使うようになる
;	自身の運命を賭けて、運まかせの大博打をすること。
;8	【狷介孤高】: %CALLNAME:OPPONENT%のダイス修正値Up
;	意志を曲げずに、他人と協力しないこと。
;9	【幽愁暗恨】: 相手のダイス修正値Down
;	誰にも知られることのない深い恨みや憂いのこと。
;10  【百載無窮】: 全ての特殊能力をあわせ持つ。最強
;	いつまでも無くなることがないこと。
;-------------------------------------------------
