;-------------------------------------------------
;이동
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM400
#DIM 입욕거부
#DIM 벌레통
#DIM 잠입해
#DIM 멈춰서대상
#DIM BUTTON돌아간다
#DIM BUTTON전환왼쪽
#DIM BUTTON전환중
#DIM BUTTON전환오른쪽
#DIM 들어가지않음
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
BUTTON돌아간다 = MAXROOM + 1
BUTTON전환왼쪽 = MAXROOM + 2
BUTTON전환중 = MAXROOM + 4
BUTTON전환오른쪽 = MAXROOM + 4
들어가지않음 = 0
ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION


;인원수 카운트를 리셋
VARSET COUNTCHARA
;방의 인원수 카운트를 설정
FOR LOCAL, 1, CHARANUM
	COUNTCHARA:(CFLAG:LOCAL:현재위치) ++
NEXT
입욕거부 = 0
CALL TURN_RESET

DRAWLINE
;
;	// 자택 맵의 표시를 신묘화 방식으로
;

CALL DRAW_MAP

PRINTFORML 현재 {(CFLAG:MASTER:현재위치)}: %NAME_FROM_PLACE(CFLAG:MASTER:현재위치)%에 있습니다

IF FLAG:짐작모드 == 1
	PRINT 색의 설명：
	CALL COLORMESSAGE("체류중　", C_H_PINK, 0)
	CALL COLORMESSAGE("업무중　", C_D_ORANGE, 0)
	CALL COLORMESSAGE("수면중　", C_N_BLUE, 0)
	CALL COLORMESSAGE("모브　　", C_D_PINK, 0)
	CALL COLORMESSAGE("자유행동", C_YELLOW, 1)
	PRINTL
ELSEIF FLAG:짐작모드 == 2
	PRINT 청결도：
	CALL COLORMESSAGE("　 고 　", C_CREAM, 0)
	CALL COLORMESSAGE("　 중 　", C_YELLOW, 0)
	CALL COLORMESSAGE("　 저 　", C_V_PINK, 0)
	CALL COLORMESSAGE("쓰레기산", C_RED, 1)
	PRINTL
ENDIF

PRINTL [  0] - 방으로 돌아간다
FOR LOCAL, MINROOM(), MAXROOM
	SIF !(CAN_MOVE(CFLAG:MASTER:현재위치, LOCAL) & 1)
		CONTINUE
	SIF NAME_FROM_PLACE(LOCAL) == ""
		CONTINUE
	SELECTCASE FLAG:이동처표시
		CASE 2
			PRINTFORML [{LOCAL, 3}] - %NAME_FROM_PLACE(LOCAL)%
		CASE 1
			PRINTFORM [{LOCAL, 3}]%NAME_FROM_PLACE(LOCAL),5,LEFT%
		CASE 0
	ENDSELECT
NEXT

IF !CAN_MOVE(CFLAG:MASTER:현재위치, ENTRANCE) & 1 && CFLAG:MASTER:현재위치 != ENTRANCE
	SIF FLAG:이동처표시 == 1
		PRINTL
	PRINTFORML [{ENTRANCE, 3}] - %NAME_FROM_PLACE(ENTRANCE)%
ELSEIF FLAG:이동처표시 == 1
	PRINTL
ENDIF
PRINTFORM [{BUTTON돌아간다, 3}] - 돌아간다　　　
PRINTBUTTON "＜]", BUTTON전환왼쪽
PRINTBUTTON "맵 전환", BUTTON전환중
PRINTFORM ({TFLAG:맵변환})
PRINTBUTTON "[＞", BUTTON전환오른쪽
SIF TALENT:MASTER:위치짐작 || TALENT:MASTER:오방짐작
	PRINTBUTTON "　　[Y] - 짐작모드", "Y"
IF FLAG:짐작모드 == 1
	PRINT (거주)　
ELSEIF FLAG:짐작모드 == 2
	PRINT (오염)　
ENDIF
SIF TALENT:MASTER:위치짐작 && FLAG:짐작모드 == 1
	PRINTBUTTON "[Z] - 있는 위치 짐작　 ", "Z"
SIF TALENT:MASTER:오방짐작 && FLAG:짐작모드 == 2
	PRINTBUTTON "[X] - 더러운 방 짐작　 ", "X"
PRINTBUTTON "[C] - 이동처 표시 전환　", "C"
IF TALENT:MASTER:체형 < -4 && !CFLAG:MASTER:동행
	IF CFLAG:MASTER:동행
		PRINTL
		PRINT (태워주니 편하다)
	ELSE
		PRINTL
		PRINT (걷는 것도 꽤 고생이다…)
	ENDIF
ENDIF

DO
	;Map Animation Code
	TOOLTIP_SETDELAY 100
	
	INPUTS

	TOOLTIP_SETDELAY 500
	;시스템계를 먼저 처리
	SELECTCASE RESULTS
	CASE "UPDATE"
		PREV_REDRAW = CURRENTREDRAW()
		REDRAW 0
		CLEARLINE LINECOUNT
		BREAK
	;察知系
	CASE "Z", "z", "X", "x"
		IF TALENT:MASTER:위치짐작 && (RESULTS == "Z" || RESULTS == "z")
			CALL HITOSAGASI()
		ELSEIF TALENT:MASTER:오방짐작 && (RESULTS == "X" || RESULTS == "x")
			CALL OHEYA_CHECK
		ENDIF
		$INPUT_LOOP
		INPUT
		IF RESULT == 0
			RESTART
		ELSEIF MAP_CAN_MOVE(RESULT) == 0
			CALL CANNOT_MOVE_MESSAGE(RESULT)
			GOTO INPUT_LOOP
		ELSEIF IN_HOME(RESULT) == 1
			CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)
			RETURN
		ENDIF
	;察知切り替え
	CASE "Y", "y"
		IF TALENT:MASTER:위치짐작 && TALENT:MASTER:오방짐작
			FLAG:짐작모드 += 1
			SIF FLAG:짐작모드 >= 3
				FLAG:짐작모드 = 0
		ELSEIF TALENT:MASTER:위치짐작
			FLAG:짐작모드 = !FLAG:짐작모드
		ELSEIF TALENT:MASTER:오방짐작
			FLAG:짐작모드 = (!FLAG:짐작모드) * 2
		ENDIF
		RESTART
	;表示切り替え
	CASE "C", "c"
		FLAG:이동처표시 += 1
		SIF FLAG:이동처표시 > 2
			FLAG:이동처표시 = 0
		RESTART
	CASE ""
		CONTINUE
	ENDSELECT

	;반각 숫자의 경우만 판정을 속행
	IF ISNUMERIC(RESULTS)
		RESULT = TOINT(RESULTS)
	ELSE
		CALL CANNOT_MOVE_MESSAGE(0)
		CONTINUE
	ENDIF
	;시스템계를 먼저 처리
	SELECTCASE RESULT
	CASE BUTTON돌아간다, CFLAG:MASTER:현재위치
		RETURN -1
	CASE BUTTON전환왼쪽
		TFLAG:맵변환 -= 1
		SIF TFLAG:맵변환 <= 0
			TFLAG:맵변환 = MAP_PAGES
		RESTART
	CASE BUTTON전환오른쪽
		TFLAG:맵변환 += 1
		SIF TFLAG:맵변환 > MAP_PAGES
			TFLAG:맵변환 = 1
		RESTART
	;私室に戻る
	CASE 0
		IF IN_HOME(CFLAG:MASTER:초기위치) != 1
			PRINTW 초기 위치가 부정확합니다. 방으로 변경합니다.
			CFLAG:MASTER:초기위치 = Default초기위치
		ENDIF
		IF MAP_CAN_MOVE(CFLAG:MASTER:초기위치,MASTER) == 0
			CALL CANNOT_MOVE_MESSAGE(CFLAG:MASTER:초기위치)
			CONTINUE
		ENDIF
		CALL SKIP_MOVE(CFLAG:MASTER:현재위치, CFLAG:MASTER:초기위치)
		RETURN
	ENDSELECT

	;남은 것은 통상의 장소의 선택 혹은 오입력

	;TW의 방침으로서 비추천이지만 박수 입력으로 이것이 있으면(자) 입력 문자수 줄어들어 편리하다
	SIF INRANGE(RESULT, 1, 99)
		RESULT += MAIN_MAP * 100
	;同じ場所はキャンセル
	IF RESULT == CFLAG:MASTER:현재위치
		RETURN -1
	;移動不可能なら弾くよ
	ELSEIF MAP_CAN_MOVE(RESULT) == 0
		CALL CANNOT_MOVE_MESSAGE(RESULT)
		CONTINUE
	;移動する
	ELSE
		CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)
		RETURN
	ENDIF
LOOP 1
;Map Animation Code

;호감도 변화 없음으로...
SIF TARGET
	TFLAG:13 = 1

;이동
@COM_ABLE400
;이동 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:우후후
	RETURN 0
SIF CFLAG:MASTER:장난
	RETURN 0
SIF CFLAG:MASTER:동침중
	RETURN 0
;자택이 아니다
SIF AT_HOME(MASTER) == 0
	RETURN 0
RETURN 1

@SKIP_MOVE(ARG, 최종목적지)
#DIM 잠정목적지, 10
#DIM 잠정목적지결정회수
#DIM 최종목적지
#DIM 직전위치
#DIM 출발지점
#DIM 경과시간
#DIM 중계지점, 20
#DIM 중계회수
#DIM RELAY_COUNT
#DIM 직전의목적지
#DIM TSP소비배율

;ARG:0	출발 위치
;ARG:1	최종목적지
VARSET LOCAL
VARSET 중계지점
잠정목적지 = 최종목적지
경과시간 = 0
중계회수 = 0
TFLAG:맵변환 = 0

출발지점 = CFLAG:MASTER:현재위치

;자동청소
CALL AUTO_SWEEP

이동캐릭터 = MASTER

[IF_DEBUG]
PRINTFORML 현재 위치 {CFLAG:MASTER:현재위치}에서 {잠정목적지}로 갈게.
[ENDIF]

IF (CAN_MOVE(CFLAG:MASTER:현재위치, 잠정목적지) & 1)
	직전위치 = ARG
	경과시간 = 1
	CFLAG:MASTER:현재위치 = 최종목적지
	RESULT = 최종목적지
	[IF_DEBUG]
	PRINTFORML 현재 위치 {CFLAG:MASTER:현재위치}에서 {잠정목적지}로 바로 이동했어.
	[ENDIF]
	GOTO STOP
ELSEIF CFLAG:MASTER:현재위치 == 최종목적지
	GOTO STOP
ELSE
	TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(최종목적지)
ENDIF

출발지점 = CFLAG:MASTER:현재위치
직전의목적지 = -1

FOR 중계회수, 0,20
	WHILE !(CAN_MOVE(출발지점, 잠정목적지) & 1)
		[IF_DEBUG]
		PRINTFORML SKIP MOVE 돌릴 거야. 출발지점 : {출발지점} / 잠정목적지 : {잠정목적지}
		[ENDIF]
		TRYCALLFORM SKIP_MOVE_{MAIN_MAP}(잠정목적지, 출발지점)
		[IF_DEBUG]
		PRINTFORML {잠정목적지}%STR:(8000 + 잠정목적지)%에 직접 갈 수 없으니 {RESULT}%STR:(8000 + RESULT)%를 목표로 할게
		[ENDIF]
		SIF 직전의목적지 == RESULT
			THROW 무한 루프가 발생하고 있습니다.
		IF RESULT == 0
			SETCOLOR 184, 38, 71
			PRINTFORML (지금 상황에서는 갈 수 없지.)
			RESETCOLOR
			GOTO STOP
		ENDIF
		직전의목적지 = 잠정목적지
		잠정목적지 = RESULT
	WEND
	[IF_DEBUG]
	SETCOLOR C_YELLOW
	PRINTFORML {중계회수+1}개째의 잠정목적지를 %NAME_FROM_PLACE(잠정목적지)%로 결정
	RESETCOLOR
	[ENDIF]
	직전의목적지 = -1
	경과시간 += 1
	중계지점:중계회수 = 잠정목적지
	SIF CAN_MOVE(잠정목적지, 최종목적지) & 1
		BREAK
	출발지점 = 잠정목적지
	잠정목적지 = 최종목적지
NEXT
직전위치 = 잠정목적지

FOR RELAY_COUNT, 1, 중계회수 + 1
	LOCAL:3 = 0
	FOR LOCAL,1,CHARANUM
		IF CFLAG:LOCAL:현재위치 == 중계지점:(RELAY_COUNT-1)
			LOCAL:3 = LOCAL
			BREAK
		ENDIF
	NEXT
	PRINTFORM {RELAY_COUNT}번째 중계지인 %NAME_FROM_PLACE(중계지점:(RELAY_COUNT-1))%
	IF LOCAL:3
		RESULT = 0
		IF CFLAG:(LOCAL:3):구상셀렉터
			TRYCALLFORM M_KOJO_K{LOCAL:3}_{CFLAG:(LOCAL:3):구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{LOCAL:3}
		ENDIF
		PRINTFORML 에서 %조사처리(CALLNAME:(LOCAL:3),"를")% 발견했다
		IF FLAG:멈춰서기 == 2
			;멈춰 서지 않는다
		ELSEIF FLAG:멈춰서기 == 1 && !RESULT
			;말이 있는 경우만
		;마녀화한 애들한테는 멈추지 않고 인사도 안 한다 들키면 좆되는걸...
		ELSEIF 16 <= LOCAL:3 && LOCAL:3 <= 27
		ELSE
			CALL ASK_M("인사하고 이동한다",1,"무시하고 이동한다", 1,"멈춰선다", 1)
			IF RESULT == 2
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,1,0)
				경과시간 = RELAY_COUNT
				CFLAG:MASTER:현재위치 = 중계지점:(RELAY_COUNT - 1)
				RESULT = 중계지점:RELAY_COUNT
				GOTO STOP
			ELSEIF RESULT == 1
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,3,0)
			ELSE
				CALL KOJO_MESSAGE_SEND("EVENT",4,LOCAL:3,2,0)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 에는 아무도 없었다
	ENDIF
NEXT

경과시간 = 중계회수 + 1
CFLAG:MASTER:현재위치 = 최종목적지
RESULT = 최종목적지

$STOP
IF IN_TOILET(RESULT)
	CALL 화장실침입(RESULT)
	SIF RESULT == -1
		CFLAG:MASTER:현재위치 = 직전위치
ELSEIF BATHCHECK(RESULT)
	LOCAL:1 = IN_ROOM_MEMBER("MIN", RESULT, "CFLAG", 6)
	IF !LOCAL:1 && !CFLAG:MASTER:동행
		PRINTFORMW 샤워실에 도착한 %마스터는% 욕탕에 잠겼다
	ELSEIF LOCAL:1 && 목욕탕추방조건(LOCAL:1) && !CFLAG:MASTER:동행
		PRINTFORMW 샤워실에 있던 %CALLNAME:(LOCAL:1)%에게 내쫓기고 말았다…
		CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
		CFLAG:MASTER:현재위치 = 직전위치
	ELSEIF LOCAL:1 && !CFLAG:MASTER:동행

		PRINTFORMW 샤워실에 있던 %조사처리(CALLNAME:(LOCAL:1),"와")% 눈이 마주쳤다…
		CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,2,0)

		CFLAG:MASTER:현재위치 = 최종목적지
	ELSE
		FOR LOCAL, 1, CHARANUM
			IF 목욕탕추방조건(LOCAL) && CFLAG:LOCAL:동행
				PRINTFORMW %조사처리(CALLNAME:LOCAL,"와")% 같이 샤워하려 했지만 거절당했다
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,1,0)
				CFLAG:MASTER:현재위치 = 직전위치
				GOTO END
			ELSEIF LOCAL:1 && 목욕탕추방조건(LOCAL:1)
				PRINTFORMW 샤워실에 있던 %CALLNAME:(LOCAL:1)%에게 내쫓기고 말았다…
				CALL KOJO_MESSAGE_SEND("EVENT",11,LOCAL:1,1,0)
				CFLAG:MASTER:현재위치 = 직전위치
				GOTO END
			ENDIF
		NEXT
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:동행
				PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 같이 샤워를 해줄 모양이다.
				CALL KOJO_MESSAGE_SEND("EVENT",23,LOCAL,2,0)
				BREAK
			ENDIF
		NEXT
		CFLAG:MASTER:현재위치 = 최종목적지
	ENDIF

ELSEIF LOCK:(RESULT)
	LOCAL:4 = RESULT

	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:311 == RESULT && CFLAG:LOCAL:수면 && !CFLAG:LOCAL:출금
			PRINTFORMW 아무래도 %조사처리(CALLNAME:LOCAL,"는")% 자고 있는 모양이다
		ELSEIF CFLAG:LOCAL:311 == RESULT && !CFLAG:LOCAL:수면 && !CFLAG:LOCAL:출금
			PRINTFORML 아무래도 %조사처리(CALLNAME:LOCAL,"는")% 자고 있는 모양이다
			PRINTFORMW 조금 소리도 들린다…
		ENDIF
		IF CFLAG:LOCAL:동행
			LOCAL:2 = 1
		ELSE
			LOCAL:2 = 0
		ENDIF
	NEXT
	IF LOCAL:2
		CFLAG:MASTER:현재위치 = 직전위치
	ELSEIF LOCAL:4 != CFLAG:MASTER:초기위치
		CALL ASK_YN("방에 숨어든다","그만둔다")
		IF RESULT == 0
			CALL 방에숨어든다(LOCAL:4, 직전위치)
			GOTO END
		ELSEIF RESULT == 1
			CFLAG:MASTER:현재위치 = 직전위치
		ENDIF
	ENDIF

ELSEIF TFLAG:연회장 == RESULT
	CALL ENKAI_SINKOU()
ENDIF
$END
[IF_DEBUG]
PRINTFORML {경과시간}번 이동했어
[ENDIF]


SIF TALENT:MASTER:100 < -4 && !CFLAG:MASTER:동행
	TIME += 7 * 경과시간
TIME += 2 * 경과시간


;21/09/01 자유행동에 대응하는 플레이버 추가
IF CFLAG:MASTER:현재위치 == FA16_HOLE:0 && !FA16_HOLE:1
	IF (50 - (ABL:MASTER:전투능력 * 7)) < RAND:100
		PRINTFORMW ……발밑에 함정이 있었지만 %마스터는% 쉽게 피했다
		FA16_HOLE:1 = 1
	ELSE
		PRINTFORMW %마스터는% 함정에 빠졌다！！
		SELECTCASE RAND(15) + TFLAG:행운보정
			CASE IS < 2
				PRINTFORML 아프다！！ 제대로 엉덩이로 떨어지고 말았다…
				CALL RECOVER(MASTER, -50, "체력", "엉덩이가 아프다")
				WAIT
		ENDSELECT
		TIME += 3
		FA16_HOLE:1 = 2
		FA16_HOLE:2 = TIME
	ENDIF
ENDIF

PREVCOM = 400
STR:0 = 이동
;NEXTCOM에 무엇인가의 값이 들어가 있는 경우는 그것을 고쳐 쓰지 않는다
;그렇지 않은 경우―1으로 리셋
SIF NEXTCOM == 0
	NEXTCOM = -1

;호감도 변화 없음으로...
SIF TARGET
	TFLAG:13 = 1
	
;-----------------------------------------------------------
;이동불능시 처리
;ARG=이동하려는 장소ID
;-----------------------------------------------------------
@CANNOT_MOVE_MESSAGE(ARG)
[IF_DEBUG]
PRINTFORML 현재 TFLAG 이동불능메시지는 {TFLAG:이동불능메시지}야.
[ENDIF]
IF !ARG || NAME_FROM_PLACE(ARG) == "" || TFLAG:이동불능메시지 == 998
	PRINTFORMW 잘못된 입력입니다
	CLEARLINE 2
	REUSELASTLINE
ELSEIF TFLAG:이동불능메시지 == 999
	PRINTFORMW 잠겨 있다
	CLEARLINE 2
	REUSELASTLINE
ELSE
	TRYCALLFORM MAP_CANNOT_WORD_{MAIN_MAP}(ARG)
ENDIF
TFLAG:이동불능메시지 = 0
