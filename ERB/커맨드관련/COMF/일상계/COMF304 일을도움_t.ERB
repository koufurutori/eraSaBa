;-------------------------------------------------
;일을돕는다
;일상계 커맨드 레벨 1
;-------------------------------------------------
@COM304
#DIM JOBTYPE

JOBTYPE = CFLAG:TARGET:직종


CALL JOB_EFFICIENCY(JOBTYPE)


	SELECTCASE JOBTYPE
		;청소
		CASE JOB_청소
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 100
			ELSE
				DOWNBASE:MASTER:체력 += 25
			ENDIF
			SIF TALENT:MASTER:청소중독
				DOWNBASE:MASTER:기력 -= 50
		;회화, 술
		CASE JOB_회화, JOB_술
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 150
			ELSE
				DOWNBASE:MASTER:체력 += 25
			ENDIF
		;전투
		CASE JOB_전투
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 200
			ELSE
				DOWNBASE:MASTER:체력 += 25
			ENDIF
		;교양
		CASE JOB_교양
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 250
			ELSE
				DOWNBASE:MASTER:체력 += 25
			ENDIF
		;요리
		CASE JOB_요리
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 150
			ELSE
				DOWNBASE:MASTER:체력 += 75
			ENDIF
		CASE JOB_음악
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 200
			ELSE
				DOWNBASE:MASTER:체력 += 50
			ENDIF
		CASE JOB_놀이
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 200
			ELSE
				DOWNBASE:MASTER:체력 += 50
			ENDIF
		CASE JOB_장사중
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 150
			ELSE
				DOWNBASE:MASTER:체력 += 75
			ENDIF
		CASE JOB_가사
			IF BASE:MASTER:기력 > 0
				DOWNBASE:MASTER:기력 += 150
			ELSE
				DOWNBASE:MASTER:체력 += 50
			ENDIF
	ENDSELECT
	;소요시간
	IF JOBTYPE == 48 || JOBTYPE == 50
		TIME += 30
	ELSE
		TIME += 60
	ENDIF
	DOWNBASE:기력 += 10
	;신뢰
	TFLAG:98 += 5
	TFLAG:무드상승억제 = 1
	;고정으로 획득하는 소스
	SOURCE:환락 = 300
	SIF JOBTYPE == 49
		SOURCE:환락 += 200
	
	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:환락 += (ABL:순종 * 50)
	ELSEIF ABL:순종 <= 3
		SOURCE:환락 += 100 + (ABL:순종 * 40)
	ELSEIF ABL:순종 <= 5
		SOURCE:환락 += 200 + (ABL:순종 * 50)
	ELSEIF ABL:순종 <= 8
		SOURCE:환락 += 400 + (ABL:순종 * 70)
	ELSEIF ABL:순종 <= 11
		SOURCE:환락 += 500 + (ABL:순종 * 60)
	ELSE
		SOURCE:환락 += 800 + (ABL:순종 * 40)
	ENDIF

	;ABL:친밀 체크
	IF ABL:친밀 <= 1
		SOURCE:환락 += 70 + (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 3
		SOURCE:환락 += 120 + (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 5
		SOURCE:환락 += 200 + (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 8
		SOURCE:환락 += 500 + (ABL:친밀 * 40)
	ELSEIF ABL:친밀 <= 11
		SOURCE:환락 += 800 + (ABL:친밀 * 40)
	ELSE
		SOURCE:환락 += 1200 + (ABL:친밀 * 30)
	ENDIF

	;호감 도를 본다
	IF CFLAG:2 <= 500
		SOURCE:환락 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:환락 < 0
		SOURCE:환락 = 0

	SOURCE:수동 = 230 + 80 * ABL:순종
	SOURCE:정복 = 150 + 200 * ABL:새드끼

SIF BASE:TARGET:일량 <= 0
	CALL CHARA_JOBEND(TARGET)
RETURN 1


@JOB_EFFICIENCY(ARG)
#DIM ABILITY
#DIM WorkP

WorkP = 0
IF GROUPMATCH(ARG, JOB_청소, JOB_회화, JOB_전투, JOB_교양, JOB_요리, JOB_음악, JOB_장사중, JOB_술, JOB_가사, JOB_채집, JOB_낚시, JOB_조제)
	SELECTCASE ARG
	CASE JOB_청소, JOB_가사
		ABILITY = ABL:MASTER:청소기능
	CASE JOB_회화, JOB_장사중, JOB_술
		ABILITY = ABL:MASTER:화술기능
	CASE JOB_전투
		ABILITY = ABL:MASTER:전투능력
	CASE JOB_교양
		ABILITY = ABL:MASTER:교양
	CASE JOB_요리
		ABILITY = ABL:MASTER:요리기능
	CASE JOB_음악
		ABILITY = ABL:MASTER:음악기능
	CASE JOB_채집
		ABILITY = TALENT:MASTER:채집Lv
	CASE JOB_낚시
		ABILITY = TALENT:MASTER:낚시Lv
	CASE JOB_조제
		ABILITY = TALENT:MASTER:조제Lv
	ENDSELECT
	SELECTCASE TFLAG:192
		;통상
		CASE 0
			LOCAL = RAND:100
			LOCAL:1 = 75 + ABILITY * 5
			IF LOCAL <= 10 + ABILITY * 2
				;기능 나름으로 대성공
				TFLAG:193 = 1
				WorkP = (50 + (ABILITY) * 75) * 2
				TFLAG:98 = 2
			ELSEIF LOCAL >= LOCAL:1
				;10～1％로 실패
				TFLAG:193 = -1
				WorkP = 0
				TFLAG:98 = -1
			ELSE
				;나머지는 성공
				TFLAG:193 = 0
				WorkP = 50 + (ABILITY) * 75
				TFLAG:98 = 0
			ENDIF
		;강제 성공 or대성공
		CASE 1
			IF RAND:100 <= 10 + ABILITY * 2
				;기능 나름으로 대성공
				TFLAG:193 = 1
				WorkP = (50 + (ABILITY) * 75) * 2
				TFLAG:98 = 2
			ELSE
				;나머지는 성공
				TFLAG:193 = 0
				WorkP = 50 + (ABILITY) * 75
				TFLAG:98 = 0
			ENDIF
		;강제 실패
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;커맨드 종료
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT
ELSEIF ARG == JOB_놀이
	SELECTCASE TFLAG:192
		;통상
		CASE 0
			LOCAL = RAND:100
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 99
				LOCAL:1 = 99
			WorkP = 0
			IF LOCAL <= 9
				;10％로 대성공
				TFLAG:193 = 1
				TFLAG:98 = 2
				EXP:MASTER:회화경험 ++
			ELSEIF LOCAL >= LOCAL:1
				;10～1％로 실패
				TFLAG:193 = -1
				TFLAG:98 = -1
			ELSE
				;나머지는 성공
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;강제 성공 or대성공
		CASE 1
			IF RAND:100 <= 9
				;10％로 대성공
				TFLAG:193 = 1
				TFLAG:98 = 2
				EXP:MASTER:회화경험 ++
			ELSE
				;나머지는 성공
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;강제 실패
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;커맨드 종료
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT
ENDIF

SELECTCASE ARG
	;청소
	CASE JOB_청소
		SIF TFLAG:193 == 1
			EXP:MASTER:청소경험 ++
		SIF !RAND:4
			EXP:MASTER:청소경험 ++
	;회화
	CASE JOB_회화, JOB_장사중, JOB_술
		SIF TFLAG:193 == 1
			EXP:MASTER:회화경험 ++
		SIF !RAND:4
			EXP:MASTER:회화경험 ++
	;전투
	CASE JOB_전투
		SIF TFLAG:193 == 1
			EXP:MASTER:전투경험 ++
		SIF !RAND:4
			EXP:MASTER:전투경험 ++
	;교양
	CASE JOB_교양
		SIF TFLAG:193 == 1
			EXP:MASTER:학습경험 ++
		SIF !RAND:4
			EXP:MASTER:학습경험 ++
	;요리
	CASE JOB_요리
		SIF TFLAG:193 == 1
			EXP:MASTER:요리경험 ++
		EXP:MASTER:요리경험 ++
	;음악
	CASE JOB_음악
		SIF TFLAG:193 == 1
			EXP:MASTER:연주경험 ++
		EXP:MASTER:연주경험 ++
	;채집
	CASE JOB_채집
		SIF TFLAG:193 == 1
			EXP:MASTER:채집경험 ++
		EXP:MASTER:채집경험 ++
	;낚시
	CASE JOB_낚시
		SIF TFLAG:193 == 1
			EXP:MASTER:낚시경험 ++
		EXP:MASTER:낚시경험 ++
	CASE JOB_조제
		SIF TFLAG:193 == 1
			EXP:MASTER:조제경험 ++
		EXP:MASTER:조제경험 ++
	;연회 정리
	CASE 50
		SIF TFLAG:193 == 1
			EXP:MASTER:청소경험 ++
		SIF !RAND:4
			EXP:MASTER:청소경험 ++
	CASE 53
		SIF TFLAG:193 == 1
			EXP:MASTER:조제경험 ++
		EXP:MASTER:조제경험 ++
ENDSELECT

;보수 배율
LOCAL:4 = TCVAR:TARGET:급료

;일한 만큼 일량 감소
LOCAL:3 = (WorkP + RAND:80 + RAND:80 + 10)

;조합지식
IF TARGET == [[메루루]]
	EXP:MASTER:조제경험 += 2
	SIF ABL:MASTER:교양 > RAND:5
		EXP:MASTER:조제경험 ++
ENDIF

;TCVAR:315가 2가 아니면 일량 줄어든다
IF TCVAR:TARGET:도울수없다 != 2
	BASE:일량 = BASE:일량 - LOCAL:3
ENDIF
IF BASE:일량 < 0 && CFLAG:TARGET:20 > 0
	BASE:일량 = 1
ELSEIF BASE:일량 < 0
	BASE:일량 = 0
ENDIF
;일한 만큼 당신의 일량을 증가(보수에 영향)
TCVAR:MASTER:도운양 += LOCAL:3 * LOCAL:4
TCVAR:TARGET:도운양 += LOCAL:3
IF ARG != 46
	PRINTFORML 해낸 일량 {LOCAL:3}
ENDIF

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE304
;일을 돕는 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(304)
	RETURN RESULT
;업무중이 아니다
SIF CFLAG:행동 != 5
	RETURN 0
;연회참가자는 별취급
SIF GROUPMATCH(CFLAG:직종, JOB_연회참가, JOB_꽐라된)
	RETURN 0
SIF TCVAR:315 == 1
	RETURN 0
;일이 끝나 있다
SIF BASE:일량 <= 0
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
;데이트중
SIF CHK_DATENOW(CFLAG:데이트중)
	RETURN 0
;기력 0

RETURN 1
