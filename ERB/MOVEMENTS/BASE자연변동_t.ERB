@BASE자연변동(ARG)
#DIM 체력회복량
#DIM 기력회복량
#DIM 욕구불만
#DIM 스트레스상승량
#DIM 임시숫자
#DIM 우산대미지
#DIM Lapse
#DIM 지저
#DIMS 低気圧位置
低気圧位置 = %GET_LOCATION_LOW()%
Lapse = TIME_PROGRESS(TIME:1)

;MASTER와 같은 장소에 없을 때
IF CFLAG:ARG:현재위치 != CFLAG:MASTER:현재위치
	BASE:ARG:무드 = BASE:ARG:무드 - Lapse * 4 * (BASE:ARG:무드 + 500) / 500
	BASE:ARG:무드 = MAX(0, BASE:ARG:무드)
	BASE:ARG:이성 = BASE:ARG:이성 + Lapse * (MAXBASE:ARG:이성 + 500) / (BASE:ARG:이성 + 500)
ELSE
	;선향옥토による補正　무드↑이성↓분노↓
	IF IsInsenceValid(ARG)
		BASE:ARG:무드 += Lapse * (SQRT(TFLAG:선향옥토) / 2)
		BASE:ARG:무드 = MIN(BASE:ARG:무드 , MAXBASE:ARG:무드)
		BASE:ARG:이성 -= Lapse * (SQRT(TFLAG:선향옥토) / 2)
		BASE:ARG:분노 -= Lapse * SQRT(TFLAG:선향옥토) / (CFLAG:ARG:빡침 + 1)
	ENDIF
ENDIF
;ガシングガーデン　이성↓욕구불만도↑ 발정付与 약독내성・비생물には効かない
IF IsGardenValid(ARG)
	BASE:ARG:이성 -= Lapse * MAX(RAND:(ABL:ARG:욕망 + 1), 4)
	욕구불만 = 욕구불만소질보정(ARG, (2 * Lapse))
	;自宅マップでないと上昇量が半減
	SIF !IN_HOME(TFLAG:GG설치위치)
		욕구불만 /= 2
	CFLAG:ARG:욕구불만도 = LIMIT(CFLAG:ARG:욕구불만도 + 욕구불만, 0, 1000)
	SIF BASE:ARG:이성 <= 0 && CFLAG:ARG:욕구불만도 >= 1000 && PALAM:ARG:욕정 >= PALAMLV:5 
		TCVAR:ARG:발정 = 1
ENDIF
BASE:ARG:이성 = LIMIT(BASE:ARG:이성, 0, MAXBASE:ARG:이성)

;업무중의 경우, 일량이 감소한다
IF CFLAG:ARG:행동 == 5
	BASE:ARG:일량 = MAX(BASE:ARG:일량 - Lapse * 10, 0)
	;연회에서 감추고 있는 경우 술기운이 상승
	SIF CFLAG:ARG:직종 == 49 && !(TALENT:ARG:술내성 == -2 )
		BASE:ARG:술기운 += Lapse * (RAND:3 * (4 - TALENT:ARG:술내성))
ENDIF
;분노는 항상 감소한다
BASE:ARG:분노 = BASE:ARG:분노 - Lapse * 5 / (CFLAG:ARG:빡침 + 1)
;연회의 자리라면 한층 더 감소한다
SIF CFLAG:ARG:직종 == 49 && CFLAG:ARG:빡침 > 0
	BASE:ARG:분노 -= Lapse * 5 / (CFLAG:ARG:빡침 + 1)
SIF BASE:ARG:분노 <= 0
	CFLAG:ARG:빡침 = 0
BASE:ARG:분노 = MAX(0, BASE:ARG:분노)
;술기운은 항상 감소한다
BASE:ARG:술기운 = BASE:ARG:술기운 - Lapse
;만취시는 감소량이 증가한다
SIF TCVAR:ARG:만취
	BASE:ARG:술기운 -= Lapse * (RAND:4) 
BASE:ARG:술기운 = MAX(0, BASE:ARG:술기운)
;수면심도의 변동
IF CFLAG:ARG:수면
	;자고 있는 경우
	TCVAR:ARG:수면심도 += Lapse * 10 * MAX(TCVAR:ARG:수면제강도, 1)
	TCVAR:ARG:수면심도 += (BASE:ARG:술기운 / 5 + TCVAR:ARG:피로도 / 3)
	TCVAR:ARG:피로도    = TCVAR:ARG:피로도 * 2 / 3
	SIF TCVAR:ARG:피로도 < 100
		TCVAR:ARG:피로도 = 0
ELSE
	;자지 않았다
	IF TCVAR:ARG:수면심도 > 0
		TCVAR:ARG:수면심도 -= Lapse * 10
	ELSE
		TCVAR:ARG:수면심도 = 0
	ENDIF
ENDIF


;언짢음
SIF CFLAG:ARG:언짢음
	BASE:ARG:분노 = MAXBASE:ARG:분노

;미약플래그
TCVAR:ARG:미약 = MAX(TCVAR:ARG:미약 - Lapse,0)
;이뇨제
TCVAR:ARG:이뇨제 = MAX(TCVAR:ARG:이뇨제 - Lapse,0)
;춘약 플래그
TCVAR:ARG:춘약 = MAX(TCVAR:ARG:춘약 - Lapse,0)
;선향옥토
IF ARG == MASTER
	TFLAG:선향옥토 = MAX(TFLAG:선향옥토 - Lapse,0)
	TFLAG:포이즌바디 = MAX(TFLAG:포이즌바디 - Lapse, 0)
ENDIF

욕구불만 = 0
IF (TCVAR:ARG:춘약 || TCVAR:ARG:발정 || IsInsenceValid(ARG)) && ARG != MASTER
	욕구불만 = 욕구불만소질보정(ARG,Lapse)
	CFLAG:ARG:욕구불만도 = LIMIT(CFLAG:ARG:욕구불만도 + 욕구불만, 0, 1000)
ENDIF
;데일리 감산 처리
CFLAG:ARG:데일리 = MAX(CFLAG:ARG:데일리 - Lapse,0)
;더러움을 붙인다
TCVAR:ARG:더럽히는양 = 0
IF AT_HOME(ARG) || CFLAG:ARG:현재위치 == OMANEKIBEYA()
	TCVAR:ARG:더럽히는양 = CFLAG:ARG:수면 ? 0 # 3 - ABL:ARG:청소기능 / (1 + TALENT:ARG:게으름)
	SIF TALENT:ARG:유치
		TCVAR:ARG:더럽히는양 += 2
	SIF CFLAG:ARG:초기위치 == CFLAG:ARG:현재위치 && TALENT:ARG:게으름
		TCVAR:ARG:더럽히는양 += 3
	SIF CFLAG:ARG:대청소 > 0
		TCVAR:ARG:더럽히는양 = CFLAG:ARG:수면 ? 0 # 1 + TALENT:ARG:유치 - 3 * (ABL:ARG:청소기능 + 1)
	;청소를 하고 있는 경우, 더러움이 감소
	IF CFLAG:ARG:직종 == 40 && CFLAG:ARG:행동 == 5
		TCVAR:ARG:더럽히는양 = -2 * (ABL:ARG:청소기능 * 20 + 100)
	;청소부는 12~18시의 사이 청소하는, 당신의 기능이 높으면 청소시켜 주지 않다
	ELSEIF TALENT:ARG:청소부 && CFLAG:ARG:신사거주 && BETWEENTIME(720,1080)
		TCVAR:ARG:더럽히는양 = -1 * MAX(ABL:ARG:청소기능 + 2 - ABL:MASTER:청소기능, 1)
	ENDIF
	YOGORE:(CFLAG:ARG:현재위치) += TCVAR:ARG:더럽히는양 * Lapse / 5
	YOGORE:(CFLAG:ARG:현재위치) = LIMIT(YOGORE:(CFLAG:ARG:현재위치), 0, 6000)
ENDIF
IF !CFLAG:ARG:우후후 && CFLAG:ARG:행동 != 5 && Lapse >= 10 && ARG
	체력회복량 = (5 + TALENT:ARG:회복속도) * Lapse / 20
	기력회복량 = (10 + TALENT:ARG:회복속도) * Lapse / 20
	;회복량이 100 미만인 경우 숨김
	CALL RECOVER(ARG, 체력회복량,"체력", 체력회복량 < 100 ? "" # "시간경과에 의한 자연 회복" )
	CALL RECOVER(ARG, 기력회복량,"기력", 기력회복량 < 100 ? "" # "시간경과에 의한 자연 회복" )
ENDIF
;쇠약의 처리
IF CFLAG:ARG:쇠약
	체력회복량 = (3 + TALENT:ARG:회복속도) * MAXBASE:ARG:체력/4000
	기력회복량 = (3 + TALENT:ARG:회복속도) * MAXBASE:ARG:기력/2000
	;동침 상태라면 상대의 회복량이 상승
	IF CFLAG:ARG:동침중
		체력회복량 *= 2
		기력회복량 *= 2
	ENDIF
	CALL RECOVER(ARG,Lapse * 체력회복량,"체력")
	CALL RECOVER(ARG,Lapse * 기력회복량,"기력")
	CFLAG:ARG:쇠약 = MAX(CFLAG:ARG:쇠약 - Lapse, 0)
	IF BASE:ARG:체력 > MAXBASE:ARG:체력 / 2
		CFLAG:ARG:쇠약 = 0
		TCVAR:ARG:휴식중 = 0
	ENDIF
ENDIF
;비, 눈이 내리고 있는 경우, 지붕이 없으면 체력이 조금씩 줄어든다
;신사 밖으로 있는 캐릭터는 줄어들지 않지만 당신과 데이트중의 상대는 줄어든다
;비로 쇠약 상태는 되지 않는다
IF OUTROOF(CFLAG:ARG:현재위치) && (AT_HOME(ARG) || ARG == MASTER || FLAG:데이트상대 == ARG)
	;당신이 접 이식 우산을 가지고 있지 않은 경우
	IF ARG == MASTER && !ITEM:접는우산
		SELECTCASE TIME:5
			;비·안개비
			CASE 4, 6
				;불필요한 타이밍에 메시지가 나오지 않게
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 비에 젖어서 쌀쌀하다
			;큰 비
			CASE 5
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 차가운 빗줄기가 피부를 찌르고 있다
			;눈·가루눈·안개눈·진눈깨비
			CASE 8, 10, 12
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 눈을 뒤집어써서 쌀쌀하다
			;폭설
			CASE 9
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 폭설에 몸이 얼어붙을 것 같다
			;(天候パッチ)豪雨・ひょうを追加、あられを変更
			;あられ
			CASE 13
				IF GET_HEAT_THUNDERSTORM() == 1
					SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
						PRINTW 싸라기눈에 힘차게 맞아서 아프다
				ELSE
					SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
						PRINTW 눈을 뒤집어써서 쌀쌀하다
				ENDIF
			;豪雨
			CASE 14
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 폭포수 같은 비가 몸에 부딪친다
			;ひょう
			CASE 15
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					PRINTW 굵은 우박이 몸을 아프게 한다
		ENDSELECT
	;당신이 접 이식 우산 을 가지고 있는과 체력 대신에 우산내구도가 줄어든다
	ELSEIF ARG == MASTER
		SELECTCASE TIME:5
			;안개비, 안개눈
			CASE 6, 12
				우산대미지 = 1
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 20
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 50
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 100
				ENDIF
			;진눈깨비
			CASE 13
				우산대미지 = 5
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 50
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 200
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 400
				ENDIF
			;비·눈·가루눈
			CASE 4, 6, 8, 10, 12
				우산대미지 = 2
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 30
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 100
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 200
				ENDIF
			;폭우·폭설
			CASE 5, 9
				우산대미지 = 20
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 50
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 200
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 400
				ENDIF
			;(天候パッチ)豪雨・ひょうを追加
			;豪雨
			CASE 14
				우산대미지 = 50
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 50
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 150
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 300
				ENDIF
			;ひょう
			CASE 15
				우산대미지 = 100
				;(天候パッチ)風速の影響
				IF WIND_VELOCITY == 1
					우산대미지 += 50
				ELSEIF WIND_VELOCITY == 2
					우산대미지 += 200
				ELSEIF WIND_VELOCITY >= 3
					우산대미지 += 400
				ENDIF
			CASEELSE
				우산대미지 = 0
		ENDSELECT
			FLAG:우산내구도 += 우산대미지 * Lapse
			IF FLAG:우산내구도 > 10000
				ITEM:접는우산 = 0
				ITEMSALES:36 ++
				FLAG:우산내구도 = 0
				ITEM:망가진우산 = 1
				SIF EQUIP:TARGET:액세서리!= 6
					TEQUIP:TARGET:한우산을씀 = 0
				CALL COLORMESSAGE(@"우산이 망가졌다!",C_YELLOW,2)
			ELSEIF FLAG:우산내구도 > 8000 && 우산대미지
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
					CALL COLORMESSAGE(@"우산이 망가질 것 같다…",C_YELLOW,1)
			ENDIF
	ENDIF
	SIF (CFLAG:ARG:우후후 == 2 && BASE:ARG:기력 > 500) || CFLAG:ARG:우후후 != 2
		BASE:ARG:체력 = MAX(BASE:ARG:체력 + 날씨에의한영향(ARG, CFLAG:ARG:현재위치) * Lapse, 500)
ENDIF

;(天候パッチ)雷・竜巻遭遇判定
IF OUTROOF(CFLAG:ARG:현재위치) && ARG == MASTER && Lapse && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
	IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && GET_WEATHER_THUNDERSTORM() == 1
		CALL COLORMESSAGE(@"천둥이 심하게 치고 있다",C_YELLOW,2)
	ELSEIF (GET_FRONTAL_THUNDERSTORM() == 1 || GET_HEAT_THUNDERSTORM() == 1) && GET_WEATHER_THUNDERSTORM() == 1
		CALL COLORMESSAGE(@"천둥소리가 들려온다",C_YELLOW,2)
	ENDIF
ENDIF

IF OUTROOF(CFLAG:ARG:현재위치) && ARG == MASTER && WIND_VELOCITY >= 2 && Lapse && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
	IF CAN_CUMULUS:2 == 2 && RAND:10 == 0
		SETCOLOR C_RED
		PRINTW 회오리바람이다！！
		RESETCOLOR
		PRINTL 
		PRINTW 회오리바람에 휘말려버려 날아가 버렸다……
		PRINTL 
		CALL ASK_DIARY("회오리바람과 조우했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/2)
	ELSEIF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && ATMOSPHERIC_INSTABILITY <= -2 && TIME:5 >= 13 && RAND:30 == 0
		SETCOLOR C_RED
		PRINTW 회오리바람이다！！
		RESETCOLOR
		PRINTL 
		PRINTW 회오리바람에 휘말려버려 날아가 버렸다……
		PRINTL 
		CALL ASK_DIARY("회오리바람과 조우했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/2)
	ENDIF
ELSEIF OUTROOF(CFLAG:ARG:현재위치) && ARG == MASTER && Lapse && !CFLAG:ARG:우후후 && !CFLAG:ARG:수면
	IF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && WIND_VELOCITY >= 3 && RAND:10 == 0
		PRINTFORML …길을 걷고 있는데 무언가가 이쪽으로 날아왔다
		SETCOLOR C_RED
		PRINTW 쓰러진 나무다！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 폭풍에 날려진 쓰러진 나무가 %CALLNAME:MASTER%에게 직격했다…
		PRINTL 
		;CALL ASK_DIARY("날아온 물건에 직격했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/2)
	ELSEIF WIND_VELOCITY >= 3 && RAND:20 == 0
		PRINTFORML …길을 걷고 있는데 무언가가 이쪽으로 날아왔다
		SETCOLOR C_RED
		PRINTW 짐수레다！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 폭풍에 날려진 짐수레가 %CALLNAME:MASTER%에게 직격했다…
		PRINTL 
		;CALL ASK_DIARY("날아온 물건에 직격했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/3)
	ELSEIF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && IS_TYPHOON:1 >= 5 && WIND_VELOCITY >= 2 && RAND:40 == 0
		PRINTFORML …길을 걷고 있는데 무언가가 이쪽으로 날아왔다
		SETCOLOR C_RED
		PRINTW 기왓장이다！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 폭풍에 날려진 기왓장이 %CALLNAME:MASTER%에게 직격했다…
		PRINTL 
		;CALL ASK_DIARY("날아온 물건에 직격했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/4)
	ELSEIF (低気圧位置 == "台風接近暴風域" || 低気圧位置 == "台風通過暴風域") && WIND_VELOCITY >= 2 && RAND:80 == 0
		PRINTFORML …길을 걷고 있는데 무언가가 이쪽으로 날아왔다
		SETCOLOR C_RED
		PRINTW 기왓장이다！！
		RESETCOLOR
		PRINTL 
		PRINTFORML 폭풍에 날려진 기왓장이 %CALLNAME:MASTER%에게 직격했다…
		PRINTL 
		;CALL ASK_DIARY("날아온 물건에 직격했다")
		BASE:MASTER:체력 -= (MAXBASE:MASTER:체력 * 1/4)
	ENDIF
ENDIF




;-------------------------------------------------
;스트레스 상승에 관하여.
;------------------------------------------------- 
;히로
IF ARG == MASTER

	;기본 스트레스 상승량은 분당 1.5. 주인공은 취침 판정 들어갔을 때 스트레스가 안 쌓이더라고...
	스트레스상승량 = Lapse * 3/2

	;에마와 화해를 못 했다면 스트레스 2.5배 상승. 단, 에마가 살아있어야 하고 유키가 원흉임을 몰라야 함.
	SIF !FLAG:에마와화해 && !FLAG:에마삭제 && !FLAG:대마녀유키
		스트레스상승량 = 스트레스상승량 * 5/2

	;에마와 화해를 못 했는데 에마가 살아있고 유키가 원흉임을 알면 죄악감으로 스트레스 3배 상승
	SIF !FLAG:에마와화해 && !FLAG:에마삭제 && FLAG:대마녀유키
		스트레스상승량 = 스트레스상승량 * 3

	;에마와 화해를 못 했는데 에마가 죽어버렸고!!! 유키가 원흉임을 알면 반쯤 미쳐서 스트레스 5배 상승
	SIF !FLAG:에마와화해 && FLAG:에마삭제 && FLAG:대마녀유키
		스트레스상승량 = 스트레스상승량 * 5

	;에마랑 화해했고 에마 살아있으면 스트레스 상승량이 0.5배로 감소. 에마는 (에슝좍 짓만 안 하면) 좋은 정신안정제임.
	SIF FLAG:에마와화해 && !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 1/2

	;에마랑 화해했는데 에마 죽으면 스트레스 상승량이 2배로 증가
	SIF FLAG:에마와화해 && FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 2

	;유키가 대마녀인 것을 안 상태에서 유키가 소환 안 됐으면 스트레스 2배 상승
	SIF FLAG:대마녀유키 && FLAG:유키삭제
		스트레스상승량 = 스트레스상승량 * 2
	
	;노아랑 싸웠고 노아가 살아 있으면 스트레스 상승량이 1.5배로 증가. 빨리 화해해.
	SIF FLAG:노아와다툼 && !FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 3/2

	;레이아가 살아있으면 스트레스 상승량이 0.7배로 감소(기대고 있는 부분이 있기 때문에). 노아나 나노카가 살아있으면 0.8배로.
	SIF !FLAG:레이아삭제
		스트레스상승량 = 스트레스상승량 * 7/10
	SIF !FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 8/10
	SIF !FLAG:나노카삭제
		스트레스상승량 = 스트레스상승량 * 8/10

	;레이아가 죽으면 스트레스 상승량이 2배로 증가(기대고 있는 부분이 있기 때문에). 노아나 나노카가 죽으면 1.5배로.
	SIF FLAG:레이아삭제
		스트레스상승량 = 스트레스상승량 * 2
	SIF FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 3/2
	SIF FLAG:나노카삭제
		스트레스상승량 = 스트레스상승량 * 3/2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;에마
ELSEIF ARG == [[에마]] && !FLAG:에마삭제

	;기본 스트레스 상승량은 분당 1.2
	스트레스상승량 = Lapse * 6/5

	;히로와 화해를 못 했다면 스트레스 2배 상승
	SIF !FLAG:에마와화해
		스트레스상승량 *= 2
	
	;유키가 대마녀인 것을 안 상태에서 유키가 소환 안 됐으면 스트레스 2.5배 상승
	SIF FLAG:대마녀유키 && FLAG:유키삭제
		스트레스상승량 = 스트레스상승량 * 3/2

	;셰리, 한나, 메루루가 살아있으면 스트레스 상승량이 0.8배로 감소. 반대로 메루루가 생존한 상태에서 흑막임이 밝혀지면 스트레스 1.5배 증가
	SIF !FLAG:셰리삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:한나삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:메루루삭제 && TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 3/2

	;셰리, 한나가 죽으면 스트레스 상승량이 1.5배로, 메루루가 죽으면 스트레스 상승량이 1.2배로 증가. 메루루가 흑막이었다 하더라도.
	SIF FLAG:셰리삭제
		스트레스상승량 = 스트레스상승량 * 3/2
	SIF FLAG:한나삭제
		스트레스상승량 = 스트레스상승량 * 3/2
	SIF FLAG:메루루삭제
		스트레스상승량 = 스트레스상승량 * 6/5

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;안안
ELSEIF ARG == [[안안]] && !FLAG:안안삭제

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = 1 * Lapse

	;노아가 살아있으면 스트레스 상승량이 0.8배로 감소. 미리아는 0.7배. 에마가 살아있으면 스트레스 증가가 2배로...
	SIF !FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:미리아삭제
		스트레스상승량 = 스트레스상승량 * 7/10
	SIF !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 2

	;노아가 죽으면 스트레스 상승량이 1.5배로 증가. 미리아는 2배. 레이아가 죽으면 1.1배.
	SIF FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 3/2
	SIF FLAG:미리아삭제
		스트레스상승량 = 스트레스상승량 * 2
	SIF FLAG:레이아삭제
		스트레스상승량 = 스트레스상승량 * 11/10

	;제 속삭임을 들어주세요오...
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 3/2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;안안은 감옥에 만족하기에 스트레스가 적게 증가한다. 0.7배로.
	스트레스상승량 = 스트레스상승량 * 7/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;노아
ELSEIF ARG == [[노아]] && !FLAG:노아삭제

	;기본 스트레스 상승량은 분당 2. 노아는 스스로를 잃은 상태이기에 스트레스를 많이 받고 있다.
	스트레스상승량 = 2 * Lapse

	;안안이 살아있으면 스트레스 상승량이 0.7배로 감소.
	SIF !FLAG:안안삭제
		스트레스상승량 = 스트레스상승량 * 7/10

	;안안이 죽으면 스트레스 상승량이 1.2배로 증가. 의외로 사람의 죽음에 대해서는 조금 담담할지도 모른다.
	SIF FLAG:안안삭제
		스트레스상승량 = 스트레스상승량 * 6/5

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;레이아
ELSEIF ARG == [[레이아]] && !FLAG:레이아삭제

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = 1 * Lapse

	;노아가 살아있으면 스트레스 증가가 1.2배, 에마는 1.5배로... 하지만 누군가 죽는다고 스트레스를 더 받지는 않음
	SIF !FLAG:노아삭제
		스트레스상승량 = 스트레스상승량 * 6/5
	SIF !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 3/2

	;제 속삭임을 들어주세요오...
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막 && !FLAG:레이아신뢰
		스트레스상승량 = 스트레스상승량 * 3/2

	;레이아 특: 짝사랑 상태일 때는 스트레스를 1.5배 더 받으나, 연인 상태로 서로 사랑을 하고 있을 때는 스트레스가 0.4배로 감소하여 굉장히 안정적
	IF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 2/5
	ELSEIF TALENT:ARG:연모
		스트레스상승량 = 스트레스상승량 * 3/2
	ENDIF

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;감옥은 좋지 않아... 레이아는 비좁은 사회에서 있어야 한다는 것이 관심적으로도, 폐소공포증 적으로도 싫다. 1.2배
	;음 이거 넣으니 완전 마녀화 머신마냥 마녀화하니까 빼야겠다
	;스트레스상승량 = 스트레스상승량 * 6/5

	;신뢰를 얻으면 스트레스 상승이 줄어든다. 아싸 리더가 또 하나!
	SIF FLAG:레이아신뢰
		스트레스상승량 = 스트레스상승량 * 1/2

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;미리아
ELSEIF ARG == [[미리아]] && !FLAG:미리아삭제
	
	임시숫자 = 1

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = 1 * Lapse

	;모두에 대해 죽을 때마다 0.1배식 상승
	FOR I,1,12
		SIF FLAG:(3000+I)
			임시숫자 += 1/10
	NEXT
	스트레스상승량 = 스트레스상승량 * 임시숫자 

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;미리아는 감옥에 나름대로 만족하기에 스트레스가 적게 증가한다. 0.8배로.
	스트레스상승량 = 스트레스상승량 * 4/5

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;마고
ELSEIF ARG == [[마고]] && !FLAG:마고삭제

	;기본 스트레스 상승량은 분당 0.5
	스트레스상승량 = Lapse * 1/2

	;제 속삭임을 들어주세요오...
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 0.7배로 감소. 여전히 사랑이 무섭기는 무서워. 그러나 짝사랑일때는 오히려 스트레스를 잔뜩 받음. 연모 2배 사모 1.5배.
	IF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 7/10
	ELSEIF TALENT:ARG:연모
		스트레스상승량 = 스트레스상승량 * 2
	ELSEIF TALENT:ARG:사모
		스트레스상승량 = 스트레스상승량 * 3/2
	ENDIF

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;나노카
ELSEIF ARG == [[나노카]] && !FLAG:나노카삭제

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = 1 * Lapse

	;간수만 안 죽으면 된다. 간수만... 언니 죽으면 스트레스 5배 치솟음...
	SIF FLAG:간수삭제
		스트레스상승량 = 스트레스상승량 * 5

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;나노카는『결의』에 차 있다...! 역시 2년이나 더 버틴 인간다워...! 라는 의미에서 0.5배.
	스트레스상승량 = 스트레스상승량 * 1/2

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;아리사
ELSEIF ARG == [[아리사]] && !FLAG:아리사삭제

	;기본 스트레스 상승량은 분당 1.5
	스트레스상승량 = Lapse * 3/2

	;에마가 살아있으면 스트레스 상승량이 0.7배로 감소. 메루루는 0.8배.
	SIF !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 7/10
	SIF !FLAG:메루루삭제
		스트레스상승량 = 스트레스상승량 * 4/5

	;에마가 죽으면 스트레스 상승량이 1.5배로 증가. 메루루는 1.3배.
	SIF FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 3/2
	SIF FLAG:메루루삭제
		스트레스상승량 = 스트레스상승량 * 13/10
	
	임시숫자 = 1

	;에마랑 메루루를 제외한 모두에 대해 죽을 때마다 0.1배식 상승
	FOR I,2,11
		SIF FLAG:(3000+I)
			임시숫자 += 1/10
	NEXT
	스트레스상승량 = 스트레스상승량 * 임시숫자 

	;제 속삭임을 들어주세요오...
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 3/2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;마조라서 징벌방 구속되면 스트레스 1.5배밖에 상승 안 해...
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 3/2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;셰리
ELSEIF ARG == [[셰리]] && !FLAG:셰리삭제

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = 1 * Lapse

	;에마와 메루루가 살아있으면 0.8배로. 한나 씨가 있나요? 그럼 딱히 됐지 않나요. 0.3배.
	SIF !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:메루루삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:한나삭제
		스트레스상승량 = 스트레스상승량 * 3/10

	;한나가 죽으면 스트레스 100배.
	SIF FLAG:한나삭제
		스트레스상승량 = 스트레스상승량 * 100

	;제 속삭임을 들어주세요오...
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 3/2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2

	;본인이 스트레스를 거부한다. 잘 풀리지도 않지만, 잘 쌓이지도 않는다. 0.1배.
	스트레스상승량 = 스트레스상승량 * 1/10

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;한나
ELSEIF ARG == [[한나]] && !FLAG:한나삭제

	;기본 스트레스 상승량은 분당 1.5
	스트레스상승량 = Lapse * 3/2

	;에마, 셰리, 메루루가 살아있으면 스트레스 상승량이 0.8배로 감소. 레이아가 살아있으면 스트레스 증가가 2배로...
	SIF !FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:셰리삭제
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:메루루삭제 && !TALENT:12:흑막
		스트레스상승량 = 스트레스상승량 * 4/5
	SIF !FLAG:레이아삭제
		스트레스상승량 = 스트레스상승량 * 2

	;에마, 셰리, 메루루가 죽으면 스트레스 상승량이 2배로 증가.
	SIF FLAG:에마삭제
		스트레스상승량 = 스트레스상승량 * 2
	SIF FLAG:셰리삭제
		스트레스상승량 = 스트레스상승량 * 2
	SIF FLAG:메루루삭제
		스트레스상승량 = 스트레스상승량 * 2

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;코코
ELSEIF ARG == [[코코]] && !FLAG:코코삭제

	;기본 스트레스 상승량은 분당 1
	스트레스상승량 = Lapse * 1

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.5배 더 쌓인다. 기분파네 코코 쨩.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 3/2

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

;메루루
ELSEIF ARG == [[메루루]] && !FLAG:메루루삭제

	;기본 스트레스 상승량은 분당 0.3
	스트레스상승량 = Lapse * 3/10

	;하지만 대마녀님이 돌아오시면 스트레스 같은 거 없어!!!!!
	SIF !FLAG:유키삭제
		스트레스상승량 = 0

	;연인 상태로 사랑을 하고 있을 때는 스트레스가 절반으로 감소
	SIF TALENT:ARG:연인
		스트레스상승량 = 스트레스상승량 * 1/2

	;어째서일까. 나는 당신을 알고 있는 것만 같아.
	SIF CFLAG:ARG:인계
		스트레스상승량 = 스트레스상승량 * 1/2
	
	;징벌방 구속되면 스트레스 2배 상승
	SIF TALENT:ARG:징벌방구속
		스트레스상승량 = 스트레스상승량 * 2

	;기분 나쁠 때는 스트레스가 1.3배 더 쌓인다.
	SIF TALENT:ARG:기분 == -1
		스트레스상승량 = 스트레스상승량 * 13/10

	;기분 좋을 때는 스트레스가 오히려 줄어든다.
	SIF TALENT:ARG:기분 == 1
		스트레스상승량 = (-1) * Lapse

	;자는 중이거나 만취했거나 섹스하고 있을 때는 스트레스 상승 안 함.
	SIF CFLAG:ARG:수면 || CFLAG:ARG:우후후 || TCVAR:ARG:만취
		스트레스상승량 = 스트레스상승량 * 0

	BASE:ARG:스트레스 += 스트레스상승량

ENDIF






;------------------------------------------
;캐릭터(ARG)가 지점(ARG:1)에 있을 때에 받는 체력에의 증감치
;이동 경향에서도 사용하므로 함수 로 분리
;------------------------------------------
@날씨에의한영향(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
;옥외, 우산 장비 이루어, 한우산을씀 하지 않는다
IF OUTROOF(ARG:1) && EQUIP:ARG:액세서리!= 6 && !TEQUIP:ARG:한우산을씀
	;당신은 우산을 가지고 있으면 제외
	SIF ARG == MASTER && ITEM:접는우산
		RETURNF 0
	SELECTCASE TIME:5
		;여름의 맑음
		CASE 0
			SIF DAY:2 == 2 && (TALENT:ARG:수서 || TALENT:ARG:빙정 == 2)
				LOCAL = -2
		;여름의 맑은 하늘
		CASE 1
			SIF DAY:2 == 2 && (TALENT:ARG:수서 || TALENT:ARG:빙정 == 2)
				LOCAL = -3
		;비·안개비
		CASE 4, 6
			IF TALENT:ARG:수서
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;눈·가루눈·안개눈·진눈깨비
		CASE 8, 10, 11, 12
			IF TALENT:ARG:빙정
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;큰 비
		CASE 5
			IF TALENT:ARG:수서
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;吹雪(天候パッチ)あられ
		CASE 9, 13
			IF TALENT:ARG:빙정
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;(天候パッチ)豪雨・ひょうを追加
		;豪雨
		CASE 14
			IF TALENT:ARG:수서
				LOCAL = 3
			ELSE
				LOCAL = -6
			ENDIF
		;ひょう
		CASE 15
			LOCAL = -10
	ENDSELECT
	;(天候パッチ)風速の影響
	IF WIND_VELOCITY == 1
		LOCAL -= 1
	ELSEIF WIND_VELOCITY == 2
		LOCAL -= 3
	ELSEIF WIND_VELOCITY >= 3
		LOCAL -= 6
	ENDIF
ENDIF

;흡혈귀와 빙정는 대미지 2배, 당신도 방위할 수 있으므로 영향을 크고
SIF LOCAL < 0 && (TALENT:ARG:흡혈귀 || TALENT:ARG:빙정 || ARG == MASTER)
	TIMES LOCAL, 2.0
RETURNF LOCAL

@IsGardenValid(ARG)
#FUNCTION
SIF CFLAG:ARG:현재위치 != TFLAG:GG설치위치
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF TALENT:ARG:인형
	RETURNF 0
SIF TALENT:ARG:약독내성
	RETURNF 0

RETURNF 1

@IsInsenceValid(ARG)
#FUNCTION
SIF !TFLAG:선향옥토
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF CFLAG:ARG:현재위치 != CFLAG:MASTER:현재위치
	RETURNF 0
SIF TALENT:ARG:인형
	RETURNF 0
SIF TALENT:ARG:약독내성
	RETURNF 0
RETURNF TFLAG:선향옥토

@IsHeat(ARG)
#FUNCTION
SIF TCVAR:ARG:미약 || TCVAR:ARG:발정 || IsGardenValid(ARG)
	RETURNF 1
RETURNF 0